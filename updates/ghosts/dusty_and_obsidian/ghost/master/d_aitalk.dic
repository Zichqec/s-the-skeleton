//---------------------------AI Talk--------------------------

AiTalk
 {
	if RAND(100) < communicateratio
 	{
 		StartCommunicate
	}
 	else
	{
 		if mikireflag == 0
		{
			if CHAIN.IDName == "" {
 				lastTalk = RandomTalk
			}
			else {
				lastTalk = ChainTalk
			}
			lastTalk
		}
 		else
		{ 
		MikireTalk
 		}
	}
 }


//--RandomTalk 

RandomTalk : nonoverlap
{
	_upperUserName = TOUPPER(username)
	"%(wdc)" //this should apply the proper balloon to _all_ AiTalk lines. 
	--
	if holdingObsidian == 1
	{
		"\0\s[630]%(wdf)AH,\w4 IF ONLY WE COULD ALL BE AS CONTENT AS YOU RIGHT NOW.%(reg)\w8\w8\1%(holdnoise)\e"
		"\1%(holdnoise)\w8\w8\0\s[630]%(wdf)YES,\w4 YOU'RE VERY SWEET.\e"
		"\1%(holdnoise)\w8\w8\0\s[630]%(wdf)WHAT WOULD I DO WITHOUT YOU?\w8\w8 HMM?\e"
		"\1%(holdnoise)\w8\w8\0\s[607]%(wdf)NO,\w4 I DON'T HAVE ANY TREATS ON ME.\w8\w8 TRY ASKING %(username).\e"
		"\1%(holdnoise)\w8\w8\0\s[607]%(wdf)YOU SURE ARE DEMANDING FOR SOMEONE WHO'S BEING PAMPERED RIGHT NOW.\e"
		"\0\s[630]%(wdf)YOU'RE GOING TO GET ME COVERED IN CAT FUR,\w4 YES YOU ARE.%(reg)\w8\w8\1%(holdnoise)\e"
		"\1%(holdnoise)\w8\w8\0\s[606]%(wdc)%(t)With how she's acting##\w4 you'd think she's desperate for me to hold her.\w8\w8\s[607\] She doesn't usually bother me to hold her when we're home##\w4 though...]\e"
		"\1%(holdnoise)\w8\w8\0\s[606]%(wdc)%(t)I tell everyone she's really very sweet##\w4 but very few actually see this side of her...\w8\w8\s[607\]\n\nFewer still gain her affection.]\e"
	}
	elseif sHoldingObsidian == 1
	{
		{
			"\0\s[205]* (He sighs contentedly.)\e"
			"\0\s[102]I'm glad Obsidian is getting some affection from someone other than me today.\e"
		}
		"\![raise,OnSHoldObsiConvos]"
	}
	elseif SomeoneHoldingObsidian != "" //If a ghost he doesn't know is holding Obsidian
	{
		"\0\s[102]%(t)She's really enjoying herself over there...]\e"
		"\0\s[104]%(t)Thank you for being gentle with her],\w4 %(SomeoneHoldingObsidian).\e"
		"\1* (She vibrates gently with a purr.)\e"
		"\1%(holdnoise)\e"
	}
	elseif watchingScreen == 1
	{
		"\![raise,OnDialogueVars,EndScreen]\0\s[107]* (He sighs and dismisses the screen.)\e"
		"\![raise,OnDialogueVars,EndScreen]\0\s[204]* (He dismisses the screen quietly.)\e"
		"\![raise,OnDialogueVars,EndScreen]\0\s[107]* (He dismisses the screen with a good natured sigh.)\e"
		"\0\s[502]%(t)Oh##\w4 that can't be good...]\e"
		"\0\s[502]%(t)Ah##\w4 please be careful...]\e"
		"\0\s[501]%(t)Hmm...]\e"
		"\0\s[551]%(t)I hope I'm not bothering you##]\w4 %(username).\w8\w8 %(t)I have so many timelines to keep up with##\w4 it's hard to let myself take breaks for too long...]\w8\w8\1%(catnoises).\e"
		"\0\s[503]%(t)Oh dear...]\e"
		"\0\s[503]%(t)They would...]\e"
		"\0\s[504]* (He sighs good naturedly.)\e"
		
		if metghosts != ""
		{
			if SUBSTR(metghosts,0,1) == ","; {metghosts = ERASE(metghosts,0,1)}
			--
			_metcount = ARRAYSIZE(metghosts)
			_rand = RAND(_metcount)
			GhostToCall = metghosts[_rand]
			
			"\![get,property,OnGhostMetCheck,ghostlist(%(GhostToCall)).name]"
		}
	}
	else
	{
		if teachWD != "Off" //Wingdings teaching mode
		{
			wingdingsles = lessonwd
			_translationproper = wingdingsles
			_translationupper = TOUPPER(wingdingsles)
			_translationlower = TOLOWER(wingdingsles)
			_wdproper = ""
			_wdupper = ""
			_wdlower = ""
			_ranCase = RandomCase
			--
			if tooltips == "On"; {_wdproper = "%(wdn)\q[%(wingdingsles),TooltipTranslation,teachword]%(rgc)"}
			else; {_wdproper = "%(wdn)%(wingdingsles)%(rgc)"}
			--
			if tooltips == "On"; {_wdupper = "%(wdn)\q[%(_translationupper),TooltipTranslation,teachword]%(rgc)"}
			else; {_wdupper = "%(wdn)%(_translationupper)%(rgc)"}
			--
			if tooltips == "On"; {_wdlower = "%(wdn)\q[%(_translationlower),TooltipTranslation,teachword]%(rgc)"}
			else; {_wdlower = "%(wdn)%(_translationlower)%(rgc)"}
			--
			"\![raise,OnQuizPick,%(_ranCase)]"
			if teachFont == "Wingdings"
			{
				_whos = WhosThatWingding
				WtWDQuiz = _whos[0]
				_descript0 = _whos[1]
				_descript1 = _whos[2]
				_n = RAND(2); if _n == 0; {_n = 2}
				"\0\s[111]Do you know which Wingding looks like %(_whos[_n])?"
				"\0\s[102]I'm thinking of a symbol that looks like %(_whos[_n]).\w8\w8\s[111] Do you know which it is?"
				"\0\s[111]There is a Wingding that looks like %(_whos[_n]),\w4 do you know which?"
				--
				"\n\n\![*]\q[Sure,OnWtWDQuiz,%(wingdingsles)]\n\![*]\q[Dismiss,cancelnodialogue]\e teachword"
			}
		}
		{
			"\0\s[208]%(t)This place is much smaller than the void##\w4 and yet it feels much larger.\w8\w8\s[107\] I guess a little variance in terrain really goes a long way.]\e"
			"\0\s[102]* (He seems content to stand in silence.)\e"
			"\0\s[109]* (He heaves a quiet sigh.)\w8\w8\n\n* (He must have a lot on his mind...)\e"
			"\1%(catnoises).\w8\w8\0\s[107]%(wdf)YES,\w4 YES,\w4 YOU'RE THE GREATEST CAT IN THE WHOLE MULTIVERSE,\w4 I KNOW.\e"
			"\1\s[10]Maaaaoow.\w8\w8\0\s[400]%(t)Yes##\w4 Obsidian##\w4 I hear you.]\e:chain=ObsiYell"
			"\0\s[107]\1\s[1001]MAAAOOOWW.\w8\w8\0\s[106]%(t)Do you have any cats##]\w4 %(username)?\w8\w8\s[402] %(t)I'm curious to know if they yell as loudly as mine.]\e"
			"\1\s[10]Myyyaaaannn.\w8\w8\0\s[400]Obsidian,\w4 %(username) %(t)looks very busy.\w8\w8 I'm sure] %(heshe)'ll %(t)give you attention later.]\1\n\n\![*]\q[Meow back,meowback]\e teachword"
			"\1\s[1001]* (Obsidian sniffs around your icons.)\w8\w8\0\s[400]%(t)She really is curious about this place.\w8\w8\s[107\]\n\nI suppose it's not often that she has so many interesting areas to explore.]\e"
			"\1* (Obsidian dis\s[-1]appears into your files.)\w8\w8\0\s[106]%(t)Ah##\w4 she'll be back eventually.\w8\w8\s[0\] She's very curious about this place##\w4\s[113\] but she craves attention##\w4 so her curiousity won't keep her away long.]\e"
			"\1\s[1004]* (Obsidian is loafing around.)\e"
			"\0\s[208]* (He's talking quietly under his breath...)\w8\w8\n\n\s[206]* (Something about timelines splitting and merging?)\e"
			"\0\s[110]%(t)I never expected I'd see a computer from the inside.\w8\w8\s[205\] It's fascinating...\w8\w8\s[208\] I should spend some time learning more about them##\w4 I wonder what might be possible here.]\e"
			"\1\s[1005]* (Obsidian begins cleaning between her toes.)\w8\w8\n\n* (Are those pieces of a %(filetype) between her claws..?)\w8\w8\0\s[106]%(t)I'm sorry] %(username),\w4 %(t)she's not usually this destructive...]\e"
			"\1\s[1001]* (Obsidian starts chewing on a pixel.)\w8\w8\n\n\0\s[402]%(wdf)CUT THAT OUT.\w8\w8 %(_upperUserName) IS BEING VERY GRACIOUS LETTING US STAY HERE,\w4 THE LEAST YOU COULD DO IS NOT DESTROY THE PLACE.\e"
			"\1\s[1001]* (Obsidian bats around some lint.)\w8\w8\n\n* (You hope that's lint,\w4 anyways,\w4 and not another %(filetype)...)\e"
			"\1\s[10]* (Obsidian seems fascinated by your cursor.)\e"
			"\1\s[1003]%(catnoise).\w8\w8\0\s[400]%(t)What's the matter##\w4 Obsidian?]\w8\w8\1\s[1004]\n\n%(catnoise)~\w8\w8\0\n\n%(t)Heh##\w4 just saying hello?]\e"
			"\1\s[1003]%(catnoise).\w8\w8\0\s[400]%(t)Yes##\w4 Obsidian?]\w8\w8\1\s[1001]\n\n%(catnoise)!\w8\w8\0\n\n%(t)Is that so?]\w8\w8\1\s[1004]\n\n%(catnoise)~\w8\w8\0\n\n%(t)Mmhm.]\e"
			"\1\s[1003]%(catnoise)!\w8\w8\0\s[400]%(t)You really like hearing the sound of your own voice##\w4 don't you?\w8\w8\s[107\] I suppose that's not unreasonable##\w4 being stuck in a place with no other sounds...]\e"
			
			{
				"\1\s[1005]Maaow~\w8\w8\0\s[402]%(wdf)"
				--
				"YOU KNOW WHAT YOU DID.\e"
				"IS THAT YOUR EXCUSE?\e"
				"AS IF I'M GOING TO FALL FOR THAT.\e"
				"IS THAT SO.\e"
				"YOU CAN'T SERIOUSLY EXPECT ME TO LET YOU OFF THAT EASY.\e"
				"YOU'RE NEVER GOING TO LEARN,\w4 ARE YOU.\e"
				"MMHMM.\e"
				"I SHOULDN'T HAVE EXPECTED ANY LESS FROM YOU.\e"
			}
		}
		
		//---Shells---
		if nowshell == "blue_sweater"
		{
			"\0\s[102]* (If you look closely,\w4 you can see a layer of cat fur covering his sweater.)\w8\w8\n\n* (He must wear it often.)\e"
			"\0\s[102]* (His sweater looks a little worn in places,\w4 but he seems very comfortable while wearing it.)\e"
			"\0\s[206]%(wdf)I WONDER IF HE'S STILL OUT THERE SOMEWHERE...%(reg)\w8\w8\1\s[1003]Meww...\w8\w8\0\s[400]%(wdf)\n\nIT'S ALRIGHT,\w4 OBSIDIAN;\w4 I'M SURE HE'S DOING WELL,\w4 EVEN IF I CAN'T FIND HIM NOW.%(reg)\e"
			"\0\s[204]%(t)Where could you be...]\e"
			"\0\s[204]%(wdf)YOU MUST BE SOMEWHERE,\w4 I STILL HAVE THE SWEATER YOU GAVE ME...%(reg)\e"
			"\0\s[200]* (He sighs,\w4 sounding a little defeated.)\w8\w8\1\s[1003]Mwwwrrr...\w8\w8\0\s[204]%(wdf)\n\nDON'T WORRY ABOUT ME,\w4 OBSIDIAN...\w8\w8 I ONLY WISH I COULD FIND SOME TRACE OF HIM SOMEWHERE,\w4 SOME SIGN HE'S STILL ALIVE AND CAUSING TROUBLE...%(reg)\e"
			"\0\s[204]%(wdf)IT'S SO EMPTY WITHOUT HIM HERE...%(reg)\e"
		}
		
		//---Hours have passed---
		if passhour >= 20
		{
			"\0\s[208]%(t)I should really be getting back soon##\w4 I need to check in with some people...]\e"
		}
		
		//---Not fed Obsidian recently---
		if recenttreats == 0
		{
			"\1\s[10]* (You hear a rumbling coming from Obsidian.)\w8\w8\n\n* (You can't tell if she's purring,\w4 or if she's hungry again...)\e"
			"\1\s[1003]MAOOWW!\w8\w8\0\s[107]%(wdf)YOU DON'T EVEN NEED TO EAT,\w4 WHY DO YOU DO THIS?\e"
		}
		//---High Obsidian Friendship---
		if ObsiLove > 100
		{
			"\1\s[1004]%(holdnoise)\w8\w8\0\s[400]%(wdf)"
			--
			"YOU REALLY LIKE IT HERE,\w4 DON'T YOU?"
			"I'M GLAD YOU ENJOY %(_upperUserName)'S COMPANY SO MUCH."
			"IT'S GOOD TO SEE YOU SO CONTENT."
			"HAVING A GOOD TIME?"
			"IT'S NICE THAT YOU'RE NOT BEGGING %(_upperUserName) FOR TREATS FOR ONCE."
			"IT'S SO PEACEFUL WHEN YOU'RE HAPPY."
			--
			"\w8\w8\1\n\n%(holdnoise)!\e"
		}
		//---Gomi, Findwin, etc...---
		if SeePC == "ON"
		{
			"\![raise,OnSnoop]" //Putting the saoris behind a raise so they don't activate every single time they talk...
		}
		
		
		
		//---Timeslots---
		if timeslot == "earlymorning" //5am-8am
		{
			if passhour > 3
			{
				"\0\s[106]%(username),\w4 %(t)have you been to sleep?\w8\w8\s[107\] I know I'm not really one to talk about getting rest##\w4 but it \f[italic\,true\]is\f[default\] important.]\e"
			}
			else
			{
				"\0\s[103]%(t)Are you enjoying the morning##]\w4 %(username)?\e"
			}
			"\0\s[102]%(t)I miss early mornings in the lab...\w8\w8 Nothing but me##\w4 my work##\w4 and a cup of coffee.]\e"
		}
		if timeslot == "morning" //9am - 11am
		{
			"\0\s[205]%(t)The world in your timeline has really come alive now##\w4 hasn't it?\w8\w8\s[102\] I do enjoy birdsong quite a bit.]\e"
			"\0\s[205]%(t)Another fine morning...\w8\w8\s[104\] Make it a good day##]\w4 %(username).\e"
		}
		if timeslot == "lunch" //12pm - 2pm
		{
			"\0\s[114]%(t)The sun is at its highest for you now##\w4 isn't it?\w8\w8\s[208\] Or##\w4 I suppose you could say that your position on your timeline's Earth is facing the sun almost directly...\w8\w8\s[106\] Depending on where you live##\w4 anyways.]\e"
			"\1\s[1004]* (Looks like the heat of the day has Obsidian sleepy.)\e"
			if recenttreats == 0
			{
				"\1\s[1003]MAAAOOW!\w8\w8\0\s[107]%(t)Obsidian##\w4 how do you even know that it's lunch time in] %(username)'s %(t)timeline right now?]\w8\w8\1\n\n* (Her stomach rumbles for treats.)\e"
			}
		}
		if timeslot == "afternoon" //3pm - 5pm
		{
			"\0\s[114]%(t)There's still plenty of daylight left.\w8\w8 What do you plan to use it for?]\e"
		}
		if timeslot == "evening" //6pm - 8pm
		{
			"\0\s[208]%(t)It's been a while since I watched a sunset.\w8\w8\s[106\] They lose some of their meaning when you can conjure one at will...]\e"
			"\0\s[102]%(t)I enjoy the sunlight##\w4 but it is soothing when the world quiets down once more...]\e"
		}
		if timeslot == "latenight" //9pm - 12am
		{
			"\0\s[106]%(t)It's getting late for you##\w4 isn't it?]\e"
			"\0\s[106]%(t)It is late where you are##\w4 isn't it?]\e"
		}
		if timeslot == "midnight" //1am - 4am
		{
			"\0\s[107]%(t)I know I shouldn't judge##\w8\s[106\] but perhaps you should think about going to sleep soon?]\e"
			"\1\s[1001]* (Obsidian yawns loudly.)\w8\w8\0\s[106]%(t)It is getting pretty late##\w4 are you planning to sleep soon?]\e"
			"\0\s[205]%(t)I love the sounds of nighttime on the surface...\w8\w8\s[102\] It's very peaceful##\w4 with the quiet sounds of wind##\w4 the nighttime bugs##\w4 sometimes a few owls...]\w8\w8\1%(catnoises).\0\w8\w8\n\n\s[113]%(t)Yes##\w4 yes.\w8\w8 Sometimes cats getting into squabbles##\w4 too.]\e"
		}
		
		//---Days of the week---
		if wd == "Sun"
		{
			"\0\s[107]%(t)The day of the week doesn't mean much to me anymore##\w4\s[0\] but it's the start of the week for you##\w4 isn't it?\w8\w8\s[104\] Make it a good one##]\w4 %(username).\e"
		}
		if wd == "Mon"
		{
			"\0\s[106]%(t)Monday has always been my favorite day##\w4 I don't like to be away from my work for too long...]\e"
		}
		if wd == "Tue"
		{
			
		}
		if wd == "Wed"
		{
			"\0\s[0]%(t)It's halfway through the week for you##\w4 right?\w8\w8\s[107\] It's hard to keep track when it varies between timelines...]\e"
		}
		if wd == "Thu"
		{
		
		}
		if wd == "Fri"
		{
			
		}
		if wd == "Sat"
		{
			"\0\s[114]%(t)It's the weekend for you##\w4 isn't it?\w8\w8\s[102\] I hope it's a relaxing one.]\e"
			"\0\s[102]%(t)It's the end of the week.\w8\w8\s[0\] And very soon##\w4 the start of a new one.\w8\w8\s[104\] What you do with that week is up to you.]\e"
		}
		
		//---Days in the month---
		if day < 5 //4th and before, start of the month
		{
			_x = ""
			if month == 1; {_x = "12th"}
			else; {_x = "12ths"}
			--
			"\0\s[114]%(t)It's the start of a new month for you##\w4 right?\w8\w8\s[102\] Hopefully it will be a peaceful one.]\e"
			"\0\s[102]%(t)Happy %(displaymonth)##]\w4 %(username).\w8\w8\s[0] %(month)/%(_x) %(t)of the way through the year now.]\e"
		}
		if day > 13 && day < 17 //14th-16th, halfway through
		{
			"\0\s[0]%(t)Halfway through the month##\w4 how are things going?]\e"
			"\0\s[102]%(t)Midway through %(displaymonth).\w8\w8\s[106\] I feel like it was the start of %(displaymonth) for you only yesterday##\w4 but then again it's very hard for me to keep track of the time across multiple timelines...]\e"
		}
		if day > 26 //27th and after, end of the month
		{
			"\0\s[102]%(t)Almost to the end of the month##\w4 time to finish up your projects if you set monthly goals.\w8\w8\n\n\s[106\]I used to##\w4 back when I lived in the Underground.\w8\w8\s[107\] There aren't many goals to set in the void.]\e"
		}
		
		//---Months---
		if month == 1
		{
			"\0\s[111]%(t)How is %(year) going for you so far?]\e"
		}
		if month == 2
		{
			
		}
		if month == 3 //Spring start Northern, Autumn start Southern
		{
			
		}
		if month == 4
		{
			
		}
		if month == 5
		{
			if day == 6
			{
				"\0\s[206]%(wdf)I'M SORRY S,\w4 I STILL HAVEN'T FOUND HIM...\e"
			}
			if day == 23 //Dusty's birthday
			{
				"\0\s[208]%(t)In your timeline##\w4 today would be my birthday.\w8\w8\s[106\] Truth be told##\w4 though##\w4 being in the void has erased any way of really tracking it.\w8\w8\s[205\] In another timeline##\w4 my birthday might be next month##\w4 or last week.\w8\w8\s[107\] Who knows when you live in a timeless dimension.]\e"
			}
		}
		if month == 6 //Summer start Northern, Winter start Southern
		{
			if day == 7 //Web's birthday
			{
				"\0\s[206]%(wdf)AH,\w4 BUT WHAT WOULD I EVEN SAY TO HIM NOW...\e"
				"\0\s[204]%(wdf)IT'S BEEN SO LONG,\w4 AND YET...\e"
				"\0\s[204]%(wdf)I WISH YOU COULD SEE ALL THAT I DID...\w8\w8 BEFORE I ENDED UP IN THE VOID,\w4 ANYWAYS...\e"
				"\0\s[204]%(wdf)I FINISHED THE CORE,\w4 I JUST WISH YOU COULD'VE BEEN THERE TO SEE IT...\e"
				"\0\s[200]%(wdf)MONSTERS HAVE A MUCH BETTER LIFE NOW,\w4 THANKS TO YOUR WORK.\w8\w8 IT'S SUCH A SHAME YOU NEVER SAW IT COME TO FRUITION...\e"
				"\0\s[200]%(wdf)I DON'T KNOW WHAT I'D DO IF HE'D ENDED UP IN THE VOID INSTEAD,\w4 THOUGH...\e"
				"\0\s[204]%(wdf)I HOPE THERE IS SOMETHING FOR YOU BEYOND,\w4 BROTHER...\e"
				"\0\s[206]%(wdf)ARE YOU WAITING IN THE INBETWEEN..?\w8\w8\s[204\] I KNOW YOU CANNOT SEE ME IN THE VOID...\e"
			}
		}
		if month == 7
		{
			
		}
		if month == 8
		{
			
		}
		if month == 9 //Autumn start Northern, Spring start Southern
		{
			
		}
		if month == 10
		{
			if day == 22 //EGCP release date!
			{
				"\0\s[208]%(t)Something is special about this day...\w8\w8\s[107\] I can't quite put my finger on what it is.\w8\w8\s[110\] The word that comes to mind is '\_a[https://www.youtube.com/watch?v=ki2Sj-N7BDk/\]echo\_a'##\w4 though I couldn't tell you why.]\e"
				"\0\s[208]%(wdf)I'M BLACK,\w4 THEN I'M WHITE...\w8\w8\n\n\s[206]NO,\w4 SOMETHING ISN'T RIGHT...%(reg)\e"
			}
		}
		if month == 11
		{
			
		}
		if month == 12 //Winter start Northern, Summer start Southern
		{
			"\0\s[0]%(t)%(year) is almost over##\w4 isn't it?\w8\w8\s[102\] I hope the new year goes well for you.]\e"
			if day == 25
			{
				"\0\s[114]%(t)Ah##\w4 it's Gyftmas for you##\w4 isn't it?\w8\w8\s[208\] Or Christmas##\w4 I suppose.\w8\w8\s[104\] Whatever holiday you celebrate##\w4 I hope you enjoy it this year.]\e"
			}
			if day == 31
			{
				"\0\s[114]%(t)You'll be starting a new year soon##\w4 wont you?\w8\w8\s[104\] I hope it brings good things for you.]\e"
			}
		}
		
		//---Seasons---
		if season == "Spring"
		{
			if month == 3 || month == 9 //First month of spring for each hemisphere
			{
				"\1\s[1000]* (Large clumps of fur litter your desktop as Obsidian grooms herself.)\w8\w8\n\n* (She must be losing her winter coat...)\e"
			}
			"\0\s[104]%(t)It's always so cheerful watching new flowers bloom.\w8\w8\s[205\] So small and fragile##\w4 but so eager to reach the sun...]\e"
			"\0\s[205]%(t)Listening to the difference between a timeline in spring and a timeline in winter is fascinating.\w8\w8\s[102\] Winter is so muted and quiet##\w4 but spring is alive and buzzing...]\e"
			"\0\s[0]%(t)Are you glad to see warm weather returning?\w8\w8\s[205\] I've never really minded any type of weather##\w4 really.]\e"
		}
		if season == "Summer"
		{
			"\1\s[1006]* (Obsidian looks sleepy in the summer heat.)\e"
			"\0\s[208]%(t)Even in the Underground##\w4 things got a lot warmer during the summer.\w8\w8\s[106\] Though##\w4 I can hardly recall what that felt like##\w4 anymore.\w8\w8\n\n\s[107\]It's warm here now##\w4 but I don't know that this is an accurate representation...]\e"
			"\0\s[205]%(t)The days in the Underground were always the same length##\w4 because the sunlight did not reach us.\w8\w8\s[201\] It's an interesting thing to observe your days changing in length.]\e"
		}
		if season == "Autumn"
		{
			"\0\s[102]%(t)Autumn is the prettiest season of all.\w8\w8\s[107\] I only wish I had better memories of it##\w4 from back when I was young...]\e"
			"\0\s[114]%(t)Do you enjoy harvest festivals##]\w4 %(username)?\w8\w8\s[205] %(t)My friends sometimes bring me food and cider##\w4\s[104\] which I appreciate greatly.]\e"
			"\0\s[205]%(t)Sometimes I like to open screens far above large forests##\w4 and just watch the colorful leaves for a while...]\e"
			"\1\s[1001]* (Obsidian starts batting a leaf around your desktop.)\w8\w8\n\n* (How did that get in here..?)\e"
		}
		if season == "Winter"
		{
			"\0\s[205]%(t)The quiet at this time of year is very relaxing.\w8\w8\s[106\] It's far different from the quiet of the void.]\e"
			"\0\s[114]%(t)You know##\w4 I can actually get extremely close to objects with my screens.\w8\w8\s[111\] I can study snowflakes in great detail##\w4 even.\w8\w8\s[205\] I should really get a camera...]\e"
			"\0\s[205]%(t)Ah##\w4 this time of year reminds me of my home in Snowdin.\w8\w8\s[102\] Those were peaceful days##\w4 for the most part...]\e"
		}
		
		//---Tempurature and weather related---
		
		//---Weather types---
		/*Types I've gathered are as follows(please let me know if you find more that aren't included here!):
		Clear
		Mostly Clear
		Sunny
		Mostly Sunny
		Fair
		
		Partly Cloudy
		Mostly Cloudy
		Cloudy
		
		Showers in the Vicinity
		Few Showers
		Showers
		Rain Shower
		Light Rain
		Rain
		Heavy Rain
		Rain and Snow
		Snow Shower
		
		Isolated Thunderstorms
		Scattered Thunderstorms
		Thunderstorm
		Heavy Thunderstorm
		
		Fog
		Haze
		*/
		
		if weather == "Clear" || weather == "Mostly Clear" || weather == "Sunny" || weather == "Mostly Sunny" || weather == "Fair" //Clear/Sunny
		{
			"\0\s[114]%(t)It seems like a nice day outside##\w4 have you been out lately?]\e"
			"\0\s[0]%(t)It seems rather pleasant out.\w8\w8\s[106\] Or perhaps it would be better if there were a few clouds to keep the heat away.]\e"
		}
		if weather == "Partly Cloudy" || weather == "Mostly Cloudy" || weather == "Cloudy" //Cloudy
		{
			"\0\s[205]%(t)Clouds are my favorite kind of weather##\w8\s[104\] they're very pretty if you look at them from above.\w8\w8\s[208\]\n\nI guess that's the silver lining of my situation;\w4 I can see all kinds of beautiful things hidden away in nature.\w8\w8\s[107\] I just can't touch them.]\e"
			"\0\s[102]%(t)In almost every place on Earth##\w4 you'll find clouds.\w8\w8\s[0\] I find that fascinating to think about.]\e"
		}
		if weather == "Showers in the Vicinity" || weather == "Few Showers" || weather == "Showers" //Might shower
		{
			"\0\s[0]%(t)It's looking like it might rain there.\w8\w8\s[114\] Do you like rain?\w8\w8\s[205\] Sometimes I open a screen to somewhere rainy just for the ambience.]\e"
		}
		if weather == "Rain Shower" || weather == "Light Rain" || weather == "Rain" || weather == "Heavy Rain" //Raining
		{
			"\0\s[102]* (He looks very relaxed...)\w8\w8\n\n* (Perhaps it's the sound of the rain.)\e"
			"\0\s[102]* (The sound of the rain seems to have soothed him.)\e"
		}
		if weather == "Rain and Snow" || weather == "Snow Shower" //Snowing -- This one is probably missing a lot atm, gotta wait til winter to gather data
		{
			"\0\s[205]%(t)I actually felt plenty of snow##\w4 back when I lived in the Underground.\w8\w8\s[102\] Ah##\w4 Snowdin was a very quaint little place...]\e"
		}
		if weather == "Isolated Thunderstorms" || weather == "Scattered Thunderstorms" || weather == "Thunderstorm" || weather == "Heavy Thunderstorm"
		{
			"\1\s[1005]* (Obsidian seems utterly unphazed by the thunder.)\e"
			"\1\s[1001]* (Obsidian ignores the thunder,\w4 seems it doesn't bother her.)\e"
			"\0\s[106]%(t)Make sure you save anything you may be working on##]\w4 %(username).\w8\w8\s[107] %(t)You never know when the power might suddenly cut##\w4 and you lose hours of important work...]\e"
		}
		if weather == "Fog" || weather == "Haze" //Limited visibility
		{
			"\0\s[108]%(t)Be careful if you go out##]\w4 %(username).\w8\w8\s[109] %(t)Reduced visibility is very serious##\w4 many a monster have lost their way in the Underground because of it...]\e"
		}
		
		//---Tempurature---
		if temp == "Below 0"
		{
			"\0\s[208]%(t)It looks very cold out there.\w8\w8\s[106\] Human skin is much more sensitive to tempurature##\w4 isn't it?\w8\w8\s[107\] I can't imagine actually going out in cold weather that way...]\e"
		}
		if temp == "Freezing"
		{
			
		}
		if temp == "Cold"
		{
			
		}
		if temp == "Chilly"
		{
			
		}
		if temp == "Pleasant"
		{
			if weather == "Clear" || weather == "Mostly Clear" || weather == "Sunny" || weather == "Mostly Sunny" || weather == "Fair" || weather == "Partly Cloudy" || weather == "Mostly Cloudy" || weather == "Cloudy"
			{
				"\0\s[0]%(t)The weather there seems pretty nice;\w4 if you've been wanting to enjoy some time outside##\w4 now might be the time to do it.]\e"
			}
		}
		if temp == "Hot"
		{
			"\0\s[106]%(t)I hope there's a breeze where you live##\w4 the heat there seems unpleasant.]\e"
		}
		if temp == "Boiling"
		{
			"\0\s[106]%(t)Be careful if you go out##]\w4 %(username).\w8\w8\s[107] %(t)It's almost as bad as Hotland out there...]\e"
		}
		
		//---Humidity---
		if humidity <= 30 //Dry
		{
			"\0\s[106]%(t)It's pretty dry out there##\w4 make sure you stay hydrated##]\w4 %(username).\e"
		}
		if humidity > 30 && humidity <= 60 //Comfortable
		{
			
		}
		if humidity >= 61 && humidity <= 80 //Muggy
		{
			"\0\s[106]%(t)It's getting pretty humid out##\w4 isn't it?\w8\w8\s[107\] I'm glad I don't have to deal with humidity in the void...]\e"
		}
		if humidity >= 81 //Soup
		{
			"\0\s[106]\1\s[1007]* (Obsidian licks at her fur,\w4 trying to smooth it down,\w4 but isn't having much luck in this humidity.)\e"
		}
		
		//---Pollen---
		if pollen == "There's a local pollen alert."
		{
			if allergicPollen == "Unknown"
			{
				"\0\s[106]%(t)There's a lot of pollen out today##\w4 are you allergic to pollen##\w4] %(username)?\n\n\![*]\q[Yes,allergicYes]\n\![*]\q[No,allergicNo]\e teachword"
			}
			if allergicPollen == "Yes"
			{
				"\0\s[107]%(t)There's a lot of pollen out there today.\w8\w8\s[106\] Take care of yourself##\w4] %(username).\e"
				"\0\s[107]%(t)Be careful if you go out today] %(username),\w4 %(t)there's a lot of pollen right now.]\e"
			}
		}
		
		//---Moon phases---
		//I've only included basic ones here, if you want more the checks would be for "Waxing Crescent", "Waxing Gibbous", "Waning Gibbous", and "Waning Crescent"
		if phase == "New Moon"
		{
			"\0\s[114]%(t)There's a new moon tonight.\w8\w8\s[104\] It's very pretty##\w4 if you can spot it.]\e"
			"\0\s[102]%(t)There's a new moon out.\w8\w8\s[0\] You should look at it##\w4 if you can##\w4 it's very lovely.]\e"
		}
		if phase == "First Quarter" || phase == "Last Quarter" //Half moon
		{
			"\0\s[114]%(t)The moon is half full tonight.\w8\w8\s[102\] It's lovely##\w4 isn't it?]\e"
			"\0\s[102]%(t)The half moon is beautiful##\w4 isn't it?]\e"
		}
		if phase == "Full Moon"
		{
			if hour >= 5 && hour <= 18
			{
				"\0\s[114]%(t)There's going to be a full moon tonight in your timeline.\w8\w8\s[102\] I was always fascinated by the moon when I lived in the Underground.]\e"
			}
			else
			{
				"\0\s[114]%(t)There's a full moon out tonight.\w8\w8 "
				"\0\s[114]%(t)The moon is full tonight.\w8\w8 "
				--
				if weather == "Mostly Cloudy" || weather == "Cloudy" || weather == "Few Showers" || weather == "Showers" || weather == "Rain Shower" || weather == "Light Rain" || weather == "Rain" || weather == "Heavy Rain" || weather == "Rain and Snow" || weather == "Snow Shower" || weather == "Isolated Thunderstorms" || weather == "Scattered Thunderstorms" || weather == "Thunderstorm" || weather == "Heavy Thunderstorm"
				{
					"\s[106\]Though##\w4 you might not be able to see it tonight ##\w4 I suppose.]\e"
					"\s[106\]It's a shame it's so cloudy.]\e"
					"\s[107\]If only it were a little clearer out.]\e"
				}
				else
				{
					"\s[104\]You should look outside and see if it's visible.]\e"
					"\s[104\]Can you see it?\w8\w8 You should look outside.]\e"
				}
			}
		}
		
		//---Noticing and commenting on other ghosts--- You MUST have an option for 'others' in the case/when, or the ghost will eventually stop talking at all! I've set it up to loop back around to aitalk, but you can put generic dialogue if you like.
		if ghostexcount > 0 //Based on code from Zarla's Gaster
		{
			_number = RAND(ghostexcount) // This checks the number of other ghosts summoned and chooses a random number based on it
			_randghostname = "%(ghostexlist[_number])" // This grabs the reference name of the ghost to check against known names.
			_skeletonlist = ""
			_i = 0
			while _i < ghostexcount
			{
				if ghostexlist[_i] == "RYS-0825" || ghostexlist[_i] == "X. Template" || ghostexlist[_i] == "S" || ghostexlist[_i] == "Gaster" || ghostexlist[_i] == "Sanguine" || ghostexlist[_i] == "Dreamtale Sans" || ghostexlist[_i] == "MVSans" || ghostexlist[_i] == "sans" || ghostexlist[_i] == "Papyrus" || ghostexlist[_i] == "Jazz"
				{
					_skeletonlist += ",%(ghostexlist[_i])"
				}
				_i++
			}
			if SUBSTR(_skeletonlist,0,1) == ","; {_skeletonlist = ERASE(_skeletonlist,0,1)} //Removes the extra comma at the start if there is one
			--
			// This will tell you the reference names of your summoned ghosts to use in the "when" cases below, along with the number of ghosts.
			// Comment it out when you no longer need it for reference.
			//"\0\s[0]Ghost List: %(ghostexlist)\n\n[half]Noticed %(_randghostname), %(_number) of %(ghostexcount)\x"
			--
			// Checking the selected loaded ghost's name vs known names to comment on them
			case _randghostname
			{
				when "RYS-0825"
				{
					"\0\s[102]* (He seems more relaxed with S around.)\e"
				}
				when "Gaster"
				{
					"\0\s[200]* (He hums quietly,\w4 and you wonder if he's a little put off by the other Gaster's presence.)\e"
				}
				when "S" //Sans and Paps
				{
					"\0\s[104]* (He waves at Sans and Papyrus,\w8\s[107] though looks a little sad.)\e"
				}
				when "Aurora.EXE"
				{
					"\0\s[208]%(t)Hmm..\w8\w8 She's stuck in computers..?\w8\w8\s[206\] It's a bit similar to being stuck in the void##\w4 in a way...]\e"
				}
				when "Temmie"
				{
					"\0\s[113]%(t)Ah##\w4 she would be here##\w4 up to silly antics as always...]\e"
				}
				others
				{
					if _randghostname _in_ metghosts
					{
						"\0\s[104]\1%(catpose)* (Obsidian meows at %(_randghostname).)\e"
					}
					else
					{
						"\![raise,OnDialogueVars,addghost,%(_randghostname)]\0\s[104]* (He waves a little at %(_randghostname).)\w8\w8\1%(catnoises).\e"
					}
				}
			}
			if _skeletonlist == ghostexlist
			{
				_skeletoncount = ARRAYSIZE(_skeletonlist)
				"\0\s[0]* (Spooky scary skeletons.)\e"
				"\0\s[0]* (Oops all skeletons.)\e"
				"\0\s[0]* (Dem bones,\w4 dem bones,\w4 dem dry bones...)\e"
				if _skeletoncount > 3
				{
					"\0\s[102]* (If you have this many skeletons on your computer,\w4 how many skeletons do you have hiding in your closet?)\e"
				}
			}
		}
		
		//---Commenting on the amount of ghosts present---
		if ghostexcount == 0
		{
			"\0\s[102]* (He seems content in the solitude.)\e"
		}
		if ghostexcount > 4
		{
			"\0\s[106]%(t)It's ah...\w8\w8 pretty crowded here##\w4 isn't it?\w8\w8\s[107\] I'm used to it just being me and Obsidian##\w4 this is a little overwhelming...]\e"
			"\1* (Obsidian dis\s[-1]appears into your files.)\w8\w8\0\s[106]%(t)Ah##\w4 sorry] %(username).\w8\w8\s[107] %(t)We're really not used to having so many people around...]\e"
			"\0\s[208]* (He looks a little antsy.)\w8\w8\n\n* (Maybe he's feeling a little crowded...)\e"
		}
		
		//---Commenting on ghosts he's met previously---
		if metghosts != ""
		{
			if SUBSTR(metghosts,0,1) == ","; {metghosts = ERASE(metghosts,0,1)}
			_metcount = ARRAYSIZE(metghosts)
			_rand = RAND(_metcount)
			//if _rand == 0; {_rand = 1}
			GhostToCall = metghosts[_rand]
			
			"\![get,property,OnGhostMetCheck,ghostlist(%(GhostToCall)).name]"
		}
		
	}
}

OnGhostMetCheck
{
	_installedlist = "%(installedghostlist)"
	
	if watchingScreen == 0
	{
		if reference0 !_in_ _installedlist
		{
			"\0\s[206]%(t)I wonder if %(GhostToCall) is doing alright...]\e"
			"\0\s[206]%(t)I hope %(GhostToCall) is doing well...]\e"
			"\0\s[206]%(t)It's been so long since I've seen %(GhostToCall)...]\e"
			"\1\s[1004]Myyrwww...\w8\w8\0\s[401]%(t)I know##\w4 Obsidian##\w4 I miss %(GhostToCall) too.]\e"
			
		}
		elseif GhostToCall !_in_ ghostexlist && reference0 _in_ _installedlist
		{
			"\0\s[208]%(t)I wonder how %(GhostToCall) is doing...]\e"
			"\0\s[208]%(t)I wonder if %(GhostToCall) is doing alright##\w8\s[502\] I should check up...]\e"
			"\0\s[208]%(t)Hmm##\w4 maybe %(GhostToCall) would know...]\e"
		}
		else
		{
			"\0\s[104]\1%(catpose)* (Obsidian meows at %(GhostToCall).)\e"
		}
	}
	else
	{
		if reference0 !_in_ _installedlist
		{
			"\0\s[501]%(t)Still no sign of %(GhostToCall)...]\e"
			"\0\s[501]%(GhostToCall),\w4 %(t)where could you be...]\e"
		}
		elseif GhostToCall !_in_ ghostexlist && reference0 _in_ _installedlist
		{
			"\0\s[502]%(t)Ah##\w4 %(GhostToCall)...]\e"
			"\0\s[503]%(t)Well##\w4 I guess at least %(GhostToCall) is doing alright...]\e"
		}
		else
		{
			"\![raise,OnAiTalk]"
		}
	}
}

OnSHoldObsiConvos //If S is holding Obsidian
{
	res_reference0 = "RYS-0825"
	
	"\0\s[104]She's really having a good time over there,\w4 huh?\e DGoodObsiTime"
	"\0\s[102]I'm glad the two of you get on so well,\w8\s[106] she can be very needy.\e DGetAlongObsi"
}


//---------------------Quizzes and choices in random dialogue---------------------

OnQuizPick
{
	if reference0 == "UPPER"
	{
		wingdingsles = lessonwd
		wingdingsles = TOUPPER(wingdingsles)
	}
	elseif reference0 == "lower"
	{
		wingdingsles = lessonwd
		wingdingsles = TOLOWER(wingdingsles)
	}
	else //Propercase
	{
		wingdingsles = lessonwd
	}
	--
	"\0\s[111]Can you translate %(wdn)%(wingdingsles)%(reg) out of %(teachFont)?\n\n\![*]\q[Sure,OnPopQuiz,%(wingdingsles)]\n\![*]\q[Dismiss,cancelnodialogue]\e teachword"
	"\0\s[111]Do you know the translation for %(wdn)%(wingdingsles)%(reg)?\n\n\![*]\q[Sure,OnPopQuiz,%(wingdingsles)]\n\![*]\q[Dismiss,cancelnodialogue]\e teachword"
	--
	"%(reference0)"
}

OnPopQuiz
{
	//"\0\s[111]\_qCan you translate %(wdn)%(wingdingsles)%(reg) out of Wingdings?\n\n\n\![*]\q[Dismiss,cancelnodialogue]\![open,inputbox,OnPopQuizAnswer]\![set,balloontimeout,-1]\*"
	"\C \![open,inputbox,OnPopQuizAnswer]\![set,balloontimeout,-1]\*"
}

OnPopQuizAnswer
{
	if reference0 == wingdingsles
	{
		G += 2
		"\0\s[0]"
		"\0\s[102]"
		"\0\s[103]"
		"\0\s[104]"
		"\0\s[114]"
		--
		"That's correct!\w8\w8\s[111] The answer was %(wingdingsles)."
		"Indeed!\w8\w8\s[111] The answer was %(wingdingsles)."
		"You are correct,\w8\s[111] The answer was %(wingdingsles)."
		"%(wingdingsles) is correct,\w4 well done."
		"Correct,\w4 %(username).\w8\w8\s[111] The answer was indeed %(wingdingsles)."
		--
		"\w8\w8\1%(catnoises).\w8\w8\n\n* (Earned 2G!)\e"
	}
	else
	{
		"\0\s[106]Sorry,\w4 that's incorrect.\w8\w8 You answered %(reference0),\w4 the answer was %(wingdingsles).\w8\w8\1%(catnoises).\e"
	}
}

OnWtWDQuiz
{
	"\C \![open,inputbox,OnWtWDQuizAnswer]\![set,balloontimeout,-1]\*"
}

OnWtWDQuizAnswer
{
	if reference0 == WtWDQuiz
	{
		G += 2
		"\0\s[0]"
		"\0\s[102]"
		"\0\s[103]"
		"\0\s[104]"
		"\0\s[114]"
		--
		"That's correct!\w8\w8\s[111] The answer was %(WtWDQuiz)."
		"Indeed!\w8\w8\s[111] The answer was %(WtWDQuiz)."
		"You are correct,\w8\s[111] The answer was %(WtWDQuiz)."
		"%(WtWDQuiz) is correct,\w4 well done."
		"Correct,\w4 %(username).\w8\w8\s[111] The answer was indeed %(WtWDQuiz)."
		--
		"\w8\w8\1%(catnoises).\w8\w8\n\n* (Earned 2G!)\e"
	}
	else
	{
		"\0\s[106]Sorry,\w4 that's incorrect.\w8\w8 You answered %(reference0),\w4 the answer was %(WtWDQuiz).\w8\w8\1%(catnoises).\e"
	}
}

Select.meowback
{
	"\1* (You meow back!)\w8\w8\n\n\s[1003]%(holdnoise)!\w8\w8\0\s[107]%(t)...You're the luckiest cat in the whole multiverse##\w4 you know that?]\e"
}

Select.allergicYes
{
	allergicPollen = "Yes"
	
	"\0\s[107]%(t)Ah##\w4 that's unfortunate.\w8\w8\s[106\] I'll let you know if there's a pollen alert in your area##\w4 then.]\e"
}

Select.allergicNo
{
	allergicPollen = "No"
	
	"\0\s[102]%(t)That's good##\w4 I'm glad that's not something you need to worry about.]\e"
}

//---Chains---
ObsiYell
{{CHAIN
	"\1\s[1001]MAAAAOOOWWW!\0\s[107]%(t)...Yes##\w4 Obsidian##\w4 we hear you##\w4 we really do.]\e"
	"\1\s[1003]%(bigf)M\w4A\w4A\w4A\w4O\w4O\w4W\w4!\0\s[402]......\w8\w8\n\n\s[109]* (He sighs quietly.)\w8\w8\n\n\s[106]%(t)I'm sorry] %(username),\w4 %(t)she does this sometimes.\w8\w8\s[107\] I hope she isn't bothering you.]\e:chain=end"
}}CHAIN

//Things that require the user's permission to do, including findwin, gomi, checking the system uptime, etc.
OnSnoop
{
	_PCuptime = (systemuptime / 3600) //systemuptime is given in seconds, this changes it to hours
	if _PCuptime > 300
	{
		"\0\s[208]%(t)Your computer has been running for %(_PCuptime) hours...\w8\w8\s[106\] That's a long time##\w4\s[107\] even for me.]\e"
	}
	if _PCuptime > 100
	{
		"\0\s[0]%(t)Your computer has been running for %(_PCuptime) hours.\w8\w8\s[102\] You must use it for a lot if you let it run so long.]\e"
	}
	
	_gomicheck = FUNCTIONEX("gomi.dll", "-n") //All of these are based on how much is in your recycling bin
	if _gomicheck == 0
	{
		"\0\s[205]%(t)It feels good to keep your recycling bin clean##\w4 doesn't it?]\e"
	}
	elseif _gomicheck >= 1000
	{
		"\![raise,OnDialogueVars,holdObsi]\1* (Obsidian wand\s[-1]ers off into your files.)\w8\w8\0\s[205]\1\n\n.\w8.\w8.\w8\w8\n\n\0\s[110]\1* (You hear a thunk,\w4 and the sound of something being torn apart.)\w8\w8\0Obsid\s[-1]ian-!\![set,sticky-window,0,1]\w8\w8\n\n.\w8.\w8.\w8\w8\n\n\s[606]%(t)I'm sorry] %(username),\w8\s[607] %(t)she got into your recycle bin...\w8\w8\s[610\] I could empty that if you want##\w4\s[606\] so she doesn't make more of a mess...]\1\n\n%(holdnoise)\e"
	}
	elseif _gomicheck >= 200
	{
		"\0\s[0]%(t)Your recycle bin has %(_gomicheck) items in it.\w8\w8\s[208\] If you'd like##\w4 I could empty that for you.]\e"
	}
	else
	{
		"\![raise,OnAiTalk]"
	}
}

Hourcheck
{
	if recenttreats != 0
	{
		recenttreats = 0
	}
	--
	if AutoWeather == "OFF"
	{
		tempurature = "Unknown"
		ForC = "Unknown"
		feelslike = "Unknown"
		humidity = "Unknown"
		wind = "Unknown"
		weather = "Unknown"
		pollen = "Unknown"
		phase = "Unknown"
	}
}

OnMinuteChange
{
	if autoupdate == "ON" && askedalready == 0 && updateavailable == 0
	{
		AutoUpdateCheck = 1
		"\![updatebymyself,checkonly]"
	}
	--
 	if minute == 0 //These check what time it is. Here, it's seeing if the minute equals zero...
	{
		if holdingObsidian == 1
		{
			"%(wdc)\0\s[600]"
		}
		else
		{
			"%(wdc)\0\s[0]"
		}
		--
 		if hour == 12 //And the hour equals 12, ie it's noon. You can change this to some other hour if you want.
		{
 			"* (It's noon.)\e"
		}
 		elseif hour == 0 //If the hour is zero, aka midnight. Again, you can change this to whatever hour you want to check for.
		{
			askedalready = 0 //resets him asking about updates
 			"* (It's midnight.)\e"
		}
		else
		{
			"* (It's %(hour12)'o clock.)\e"
		}
	}
	elseif mikireflag == 1
 	{
 		MikireTalk
	}
	--
	if (SinceTalk >= 30 && AutoWeather == "ON" && weatherquiet == 2) || passmin == 0 && AutoWeather == "On"
	{
		weatherquiet = 1
		"\![raise,OnDownloadCurWeather]"
	}
	--
}

OnSecondChange
{
	if passsec >= 59
 	{
	 	passsec = 0
 		passmin++
		totalminutes++
		if passmin >= 59
		{
			passmin = 0
			passhour++
			Hourcheck
		}
		if totalminutes >= 59
		{
			totalminutes = 0
			totalhours++
		}
	}
 	else
	{
		passsec++
	}
	if commuTimer >= 1; commuTimer--; else; commuTimer = 0
	if commuTimer == 0; AlreadyResponded = 0
	--
	if SHIORI3FW.CanTalk == 1 //This gets the seconds since the last dialogue ended
	{
		SinceTalk++
	}
	else
	{
		SinceTalk = 0
	}
	--
	if mikireflag == 0 && reference1 == 1 //Dialogue for putting them off the screen. You could make this a unique line if you wanted, and have MikireTalk be for if they continue to be offscreen
	{
		MikireTalk
	}
	if mikireflag == 1 && reference1 == 0 //Dialogue for coming back on the screen
	{
		EndMikireTalk
	}
	mikireflag = reference1 //reference1 is if the ghost is offscreen or not
	--
	if reference2 == 1 //When overlapping
	{
		if holdingObsidian == 0
		{
			pickedUp++
			"\![raise,OnDialogueVars,holdObsi]\![set,sticky-window,0,1]\1\s[-1]\0%(wdc)"
			--
			"\s[630]%(wdf)HELLO,\w4 OBSIDIAN.%(reg)"
			"\s[630]%(wdf)HELLO,\w4 SWEET GIRL.%(reg)"
			"\s[630]%(wdf)STAYING WITH ME FOR A WHILE?%(reg)"
			"\s[600]%(t)Ah##\w4 alright##\w4 I'll hold her for a bit.]"
			"\s[600]%(t)Ok##\w4 I can hold her for a bit.]"
			"\s[606]%(t)Ah##\w4 she's not in your way##\w4 is she?]"
			--
			"\w8\w8\1%(holdnoise)"
		}
	}
}
/* //Oopsie I forgot to finish this. I'll mess with it in a future update.
OnOffscreen
{
	"%(reference0)"
}
*/
MikireTalk
{
	if holdingObsidian == 1
	{
		"\0\s[610]%(wdc)%(t)Ah##\w4 %(username)?\w8\w8\s[606\] We're off the edge of the screen.]\w8\w8\n\n%(rg)* (You can press ctrl shift f12 to bring him back.)\w8\w8\1%(holdnoise)\e"
	}
	else
	{
		"\0\s[110]%(wdc)%(t)Ah##\w4 %(username)?\w8\w8\s[106\] I'm off the edge of the screen.]\w8\w8\n\n%(rg)* (You can press ctrl shift f12 to bring him back.)\w8\w8\1%(annoyednoises).\e"
	}
}

EndMikireTalk
{
	if holdingObsidian == 1
	{
		"\0\s[606]%(wdc)%(t)Thank you##]\w4 %(username).\1%(holdnoise)\e"
	}
	else
	{
		"\0\s[106]%(wdc)%(t)Thank you##]\w4 %(username).\1%(catnoises).\e"
	}
}

OnSurfaceRestore
{
	if holdingObsidian == 1
	{
		if RAND(100) < 59
		{
			"\1\s[-1]\0\s[600]"
		}
		else
		{
			holdingObsidian = 0
			jumpMenu = 0
			"\![get,property,OnObsidianJumpDown,currentghost.scope(0).rect]"
		}
	}
	elseif sHoldingObsidian == 1
	{
		"\![raise,OnObsiReturnS]"
		{
			"\0\s[0]\e"
			"\0\s[102]\e"
			"\0\s[204]\e"
			"\0\s[205]\e"
			"\0\s[208]\e"
		}
	}
	elseif SomeoneHoldingObsidian != ""
	{
		{
			"\0\s[0]\e"
			"\0\s[102]\e"
			"\0\s[204]\e"
			"\0\s[205]\e"
			"\0\s[208]\e"
		}
		"\![raise,OnObsiReturnUnknown]"
	}
	elseif watchingScreen == 1
	{
		"\0\s[599]\1%(catpose)\e"
		"\0\s[599]\1%(catpose)\e"
		"\![raise,OnDialogueVars,EndScreen]"
	}
	elseif reference1 == -1 && sHoldingObsidian != 1 && SomeoneHoldingObsidian == "" //If Obsidian is hidden and not being held by Dusty (ie, she's off in your files somewhere)
	{
		"\0\s[0]"
		"\0\s[102]"
		"\0\s[204]"
		"\0\s[205]"
		"\0\s[208]"
		if RAND(100) > 50
		{
			"\![raise,OnDialogueVars,holdObsi]\0\s[110]\1* (You hear a strange noise coming from your files.)\w8\w8\0%(t)Obsid\s[-1\]ian-!\w8\w8\n\n.\w8.\w8.\w8\w8\n\n\s[606\]Sorry##]\w4 %(username). \w8\w8\s[607]%(t)She was chewing on a %(musicfile).]\w8\w8\1\n\n%(holdnoise)\e"
			"\1MAAOOOWWW!!\w8\w8\0\s[106]%(t)Even at this distance##\w4 she can still be so loud...]\e"
			"\1* (You wonder how much cat fur must be stuck in your files by now...)\e"
			"\1\s[1007]* (Obsidian reemerges from your files.)\w8\w8\n\n* (Those pixels stuck to her fur...\w8\w8 Are they from a %(filetype)?)\e"
			if totalpingpong > 0
			{
				"\0\s[106]\1* (You hear the sound of a ping pong ball rolling around in your files somewhere...)\e"
			}
			{
				_capfile = TOUPPER(filetype)
				"\![raise,OnDialogueVars,holdObsi]\1%(catnoise)...\w8\w8\0\s[106]%(t)Did you get stuck..?]\w8\w8\s[-1]\![set,sticky-window,0,1]\n\n.\w8.\w8.\n\n\w8\w8\w8%(wdf)"
				--
				"HOLD STILL..."
				"STOP SQUIRMING..!"
				"SHEATHE YOUR CLAWS..!"
				"HOLD STILL AND LET ME GET YOU OUT OF HERE."
				"OBSIDIAN,\w4 I CAN'T HELP YOU IF YOU'RE MOVING AROUND."
				"DON'T YOU POINT THOSE CLAWS AT ME."
				"AH-!\w8 HEY!"
				"I'M TRYING TO HELP YOU,\w4 STOP IT!"
				"TSK,\w4 HOW DO YOU GET YOURSELF INTO THESE THINGS..."
				"REALLY?\w8\w8 YOU'RE CAUGHT ON A %(_capfile)?"
				"HOW DID YOU MANAGE TO GET STUCK IN A %(_capfile)?"
				"YOU THINK YOU'D LEARN EVENTUALLY..."
				--
				"\w8\w8\n\n\s[607]%(reg)* (He sighs.)\w8\w8\1\n\n%(holdnoise)\e"
			}
		}
	}
	else //Normal
	{
		"\0\s[0]"
		"\0\s[102]"
		"\0\s[204]"
		"\0\s[205]"
		"\0\s[208]"
		"\0\s[400]"
		"\0\s[402]"
		"\0\s[599]\![raise,OnDialogueVars,StartScreen]"
		--
		"\1%(catpose)\e"
	}
}

OnObsidianJumpDown //Obsidian jumping out of Dusty's arms
{
	_XCoord = TOINT(reference0[0])
	_XCoord -= 20
	_toX = (_XCoord - 100)
	_YCoord = TOINT(reference0[3])
	_YCoord = (_YCoord - 120) //Go up by half Obsi's frame height
	if nowscale > 100
	{
		_YCoord = (_YCoord - nowscale)
	}
	if nowscale < 100
	{
		_YCoord = (_YCoord + nowscale)
	}
	
	"\![reset,sticky-window]\0\s[490]\w4\1\s[1009]\![move,--X=%(_XCoord),--Y=%(_YCoord),--base=primaryscreen]\![move,--X=%(_toX),--Y=fix,--time=700,--base=primaryscreen]\s[1004]%(holdnoise)\w8\w8\w8\0\s[400]"
	--
	"%(wdf)ALRIGHT THEN."
	"%(wdf)OK."
	"%(wdf)UP TO YOU."
	"%(wdf)TOO MUCH FOR NOW?"
	--
	if jumpMenu == 0 //If you weren't trying to open the menu
	{
		"\e"
	}
	else
	{
		jumpMenu = 0
		holdingObsidian = 0
		"\w8\w8%(reg)\n\n"
		--
		"\s[106]%(t)Alright##\w4 what did you need?]"
		"\s[106]%(t)Sorry about that##\w4 what did you need?]"
		--
		MenuRun
	}
	--
	"Start: %(_XCoord) Fin: %(_toX) YCoord: %(_YCoord)  %(reference0[3])"
}

OnObsiReturnS
{
	//"\![get,property,OnObsidianJumpDownS,currentghost.scope(0).rect]"
	res_reference0 = "RYS-0825"
	
	"\e SObsiJumpDown01"
}

OnObsiReturnUnknown
{
	_ghost = SomeoneHoldingObsidian
	res_reference0 = SomeoneHoldingObsidian[0]
	
	"\e %(_ghost)ObsiJumpDown01"
}

OnKeyPress
{
	if reference0 == "t"
	{
		OnAiTalk
	}
	elseif reference0 == "r"
	{
		OnLastTalk
	}
	elseif reference0 == "f1"
	{
		"\![open,readme]"
	}
	elseif reference0 == "v" && debug == 1
	{
		OnTestVariable
	}
}