//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\0\s[0]\![set,balloontimeout,-1]"
	--
	if nowmode == "Awake"
	{
		"\0%(b)\s[1110]Oh,\w4 you want to change your name?\w8\w8\s[1104] Sure,\w4 what should I call you?"
	}
	else //Sleepy
	{
		"\0%(b)\s[105]...what should i call you..?"
	}
	--
	"\![open,inputbox,OnNameTeach,-1]"
}

OnNameTeach
{
	tempname = reference0 //this stores the original name in tempname
	reference0 = TOLOWER(reference0) //changes it to lowercase
	reference0 = REPLACE(reference0," ","") //removes spaces
	reference0 = REPLACE(reference0,".","") //removes periods
	reference0 = REPLACE(reference0,"-","") //removes dashes
	
	if tempname == username //If the user gives the exact same name as before
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1113]...Isn't that the name you gave me before?"
		}
		else //Sleepy
		{
			"\0%(b)\s[112]...that's the same as before,\w4 isn't it..?"
		}
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	elseif reference0 == "lulo"
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1116]...That's my name,\w4\s[1106] that would get confusing.\![open,inputbox,OnNameTeach,-1]\e"
		}
		else //Sleepy
		{
			"\0%(b)\s[107]...that's my name...\![open,inputbox,OnNameTeach,-1]\e"
		}
	}
	elseif reference0 == "" //If the user doesn't put in anything
	{
		username = "user"
		userprop = "User"
		
		if nowmode == "Awake"
		{
			"\0%(b)\s[1106]Oh,\w4 um,\w4 I guess I'll just call you User,\w4 then?\w8\w8\s[1104] Let me know if you want to change it."
		}
		else //Sleepy
		{
			"\0%(b)\s[107]...um,\w4 i'll call you user,\w4 then...\w8\w8\s[105] let me know if you want to change it..."
		}
	}
	else //if it's none of these above names, we can use it. You can add names that just have comments and can still be used, just don't add a \e at the end.
	{
		username = TOLOWER(tempname)
		userprop = tempname
		
		if nowmode == "Awake"
		{
			"\0%(b)\s[1104]Ok,\w4 %(userprop) it is!"
		}
		else //Sleepy
		{
			"\0%(b)\s[0]...ok,\w4 i'll call you %(username)..."
		}
	}
	--
	if booting == 0
	{
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //This is the end of the FirstBoot dialogue!
	{
		booting = 0
		
		if SInstalled //this might be an issue if you've been cruel to S but OH WELL
		{
			"\s[111]\w8\w8\n\n...i've heard of you...\w8\w8\s[112] '%(username)'...\w8\w8\w8\s[111] you bring my dad here,\w4 don't you..?\w8\w8"
			if SPresent
			{
				" \s[112] .\w2.\w2.\w2oh,\w4 hey dad..."
			}
			else
			{
				"\s[0] it's nice to meet you..."
			}
		}
		else
		{
			"\s[0]\w8\w8 hi..."
		}
		--
		"\w8\w8\n\n\s[113]...um,\w4 i'm not sure how much you'll want to hang out with me,\w4 i just sleep a lot,\w4 mostly...\w8\w8\s[108] let me know if you need anything,\w4 i guess..?\![leave,passivemode]\e"
	}
}

OnChangePronoun //Envelope names are the same as the GT template, since I figured most people will be used to them.
{
	if nowmode == "Awake"
	{
		"\0%(b)\s[1104]Sure,\w4 how should I refer to you?"
	}
	else
	{
		"\0%(b)\s[112]...how do you want me to refer to you..?"
	}
	--
	"\n\n\_q/
	\![*]\q[He/Him/His,OnPronouns,masc]\n/
	\![*]\q[She/Her/Hers,OnPronouns,fem]\n/	
	\![*]\q[They/Them/Their,OnPronouns,undef]\e"
}


OnPronouns
{
	if reference0 == "masc"
	{
		heshe = "he"
		himher = "him"
		hisher = "his"
		hesshes = "he's"
	}
	elseif reference0 == "fem"
	{
		heshe = "she"
		himher = "her"
		hisher = "her"
		hesshes = "she's"
	}
	else
	{
		heshe = "they"
		himher = "them"
		hisher = "their"
		hesshes = "they're"
		
	}
	--
	if nowmode == "Awake"
	{
		"\0%(b)\s[1104]Alright then,\w4 %(heshe),\w4 %(himher),\w4 and %(hisher) it is!"
	}
	else //Sleepy
	{
		"\0%(b)\s[0]...ok..."
	}
	--
	"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
}
//Pronoun examples for my own sake:
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."

OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	if nowmode == "Awake"
	{
		if birthdayprint == "???"
		{
			"\0%(b)\s[1110]Oh,\w4 your birthday?\w8\w8\s[1104] What month were you born in?"
		}
		else
		{
			"\0%(b)\s[1113]Oh,\w4 did I get your birthday wrong..?\w8\w8\s[1106] What month were you born in?"
		}
	}
	else //Sleepy
	{
		"\0%(b)\s[105]...birthday...\w8\w8\s[111] what month were you born in..?"
	}
	--
	"\![open,inputbox,OnUserBornMonth,-1]"
}

OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	if _tempmonth == "january" || _tempmonth == "1" {bornmonth = 1; bornmonthprint = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {bornmonth = 2; bornmonthprint = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {bornmonth = 3; bornmonthprint = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {bornmonth = 4; bornmonthprint = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {bornmonth = 5; bornmonthprint = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {bornmonth = 6; bornmonthprint = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {bornmonth = 7; bornmonthprint = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {bornmonth = 8; bornmonthprint = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {bornmonth = 9; bornmonthprint = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {bornmonth = 10; bornmonthprint = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {bornmonth = 11; bornmonthprint = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {bornmonth = 12; bornmonthprint = "December"}
	else //if the month isn't a valid month, like 32 or something.
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1107]...%(_tempmonth) isn't a real month,\w4 c'mon."
		}
		else //Sleepy
		{
			"\0%(b)\s[107]...a real month,\w4 please..."
		}
		--
		"\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
	}
	--
	//If it's a valid month
	if nowmode == "Awake"
	{
		"\0%(b)\s[1111]Ok,\w4 %(bornmonthprint).\w8\w8 What day?"
	}
	else //Sleepy
	{
		_lowmonth = TOLOWER(bornmonthprint)
		
		"\0%(b)\s[105]...%(_lowmonth)...\w8\w8\s[0] what day..?"
	}
	--
	"\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\e"
}

OnUserBornDay
{
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	
	if ((bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12) && _tempday >= 1 && _tempday <= 31) || (bornmonth == 2 && _tempday >= 1 && _tempday <= 29) || ((bornmonth == 4 || bornmonth == 6 || bornmonth == 9 || bornmonth == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range. If it does, we set some variables and continue down.
	{
		bornday = _tempday
		borndayprint = Printday
		userbirthday = "%(bornmonth)/%(bornday)" //If you want to write dates as DD/MM, feel free to swap this around
		birthdayprint = "%(bornmonthprint) %(borndayprint)"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1113]...You have to pick a real day.\w8\w8\s[1106] Try again."
		}
		else //Sleepy
		{
			"\0%(b)\s[107]...it has to be a real day..."
		}
		--
		"\![open,inputbox,OnUserBornDay,-1]\e"
	}
	-- 
	//All of these only show if it's a valid birthday
	if userbirthday == "%(month)/%(day)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1110]Oh,\w4 today?\w8\w8\s[1104] Happy birthday,\w4 %(userprop)!"
		}
		else //Sleepy
		{
			"\0%(b)\s[111]...today..?\w8\w8\s[103] happy birthday..."
		}
	}
	elseif userbirthday == "2/26" //Lulo's birthday
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1110]Oh!\w8\w8\s[1104] You have the same birthday as me,\w4 that's cool."
		}
		else //Sleepy
		{
			"\0%(b)\s[111]...that's my birthday,\w4 too..."
		}
	}
	else //if the birthday is not listed above.
	{
		if nowmode == "Awake"
		{
			"\0%(b)\s[1000]Ok,\w4 %(bornmonthprint) %(borndayprint).\w8\w8\s[1104] I'll remember that."
		}
		else //Sleepy
		{
			_lowmonth = TOLOWER(bornmonthprint)
			"\0%(b)\s[0]...%(_lowmonth) %(borndayprint)...\w8\w8 ok..."
		}
	}
	--
	"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
}

Printday //Sets the appropriate suffix
{
	if bornday == 1 || bornday == 21 || bornday == 31; "%(bornday)st"
	elseif bornday == 2 || bornday == 22; "%(bornday)nd"
	elseif bornday == 3 || bornday == 23; "%(bornday)rd"
	else; "%(bornday)th"
}	