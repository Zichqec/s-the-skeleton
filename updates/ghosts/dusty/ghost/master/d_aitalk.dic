//---------------------------AI Talk--------------------------

AiTalk
 {
	if RAND(100) < communicateratio
 	{
 		StartCommunicate
	}
 	else
	{
 		if mikireflag == 0
		{
			if CHAIN.IDName == "" {
 				lastTalk = RandomTalk
			}
			else {
				lastTalk = ChainTalk
			}
			lastTalk
		}
 		else
		{ 
		MikireTalk
 		}
	}
 }


//--RandomTalk 

RandomTalk : nonoverlap
{
	"%(wdc)" //this should apply the proper balloon to _all_ AItalk lines. 
	--
	//"\0\s[0]Here are some wingdings.\w8\w8\n\n%(dings)Wingdings.%(dfont)\e"
	
	_wingdings = lessonwd
	_translationproper = _wingdings
	_translationupper = TOUPPER(_wingdings)
	_translationlower = TOLOWER(_wingdings)
	_wdproper = ""
	_wdupper = ""
	_wdlower = ""
	--
	if tooltips == "On"
	{
		_wdproper = "%(wdn)\q[%(_wingdings),TooltipTranslation,teachword]%(rgc)"
	}
	else
	{
		_wdproper = "%(wdn)%(_wingdings)%(rgc)"
	}
	--
	if tooltips == "On"
	{
		_wdupper = "%(wdn)\q[%(_translationupper),TooltipTranslation,teachword]%(rgc)"
	}
	else
	{
		_wdupper = "%(wdn)%(_translationupper)%(rgc)"
	}
	--
	if tooltips == "On"
	{
		_wdlower = "%(wdn)\q[%(_translationlower),TooltipTranslation,teachword]%(rgc)"
	}
	else
	{
		_wdlower = "%(wdn)%(_translationlower)%(rgc)"
	}
	--
	//------------------------------------------------
	if holdingObsidian == 1
	{
		"\0\s[600]The word %(_translationproper) written in uppercase looks like this:\n%(_wdupper)\w8\w8\n\nWritten in lowercase,\w4 it looks like this:\n%(_wdlower)\w8\w8\1%(holdnoise)\e"
	}
	else
	{
		"\0\s[111]The word %(_translationproper) written in uppercase looks like this:\n%(_wdupper)\w8\w8\n\nWritten in lowercase,\w4 it looks like this:\n%(_wdlower)\w8\w8\1\s[10]%(catnoises).\e"
	
		
	
	}
}




Hourcheck
{
	if recenttreats != 0
	{
		recenttreats = 0
	}
}

OnMinuteChange
{
	if autoupdate == "ON" && askedalready == 0 && updateavailable == 0
	{
		AutoUpdateCheck = 1
		"\![updatebymyself,checkonly]"
	}
	--
 	if minute == 0 //These check what time it is. Here, it's seeing if the minute equals zero...
	{
		Hourcheck
		--
		if holdingObsidian == 1
		{
			"%(wdc)\0\s[600]"
		}
		else
		{
			"%(wdc)\0\s[0]"
		}
		--
 		if hour == 12 //And the hour equals 12, ie it's noon. You can change this to some other hour if you want.
		{
 			"* (It's noon.)\e"
		}
 		elseif hour == 0 //If the hour is zero, aka midnight. Again, you can change this to whatever hour you want to check for.
		{
			askedalready = 0 //resets him asking about updates
 			"* (It's midnight.)\e"
		}
		else
		{
			"* (It's %(hour12)'o clock.)\e"
		}
	}
	//Don't touch this part.
	elseif mikireflag == 1
 	{
 		MikireTalk
	}
}

OnSecondChange
{
	if passsec >= 59
 	{
	 	passsec = 0
 		passmin++
		totalminutes++
		if passmin >= 59
		{
			passmin = 0
			passhour++
		}
		if totalminutes >= 59
		{
			totalminutes = 0
			totalhours++
		}
	}
 	else
	{
		passsec++
	}
	if mikireflag == 1 && reference1 == 0
	{
		EndMikireTalk
	}
	mikireflag = reference1
	if reference2 == 1 //When overlapping
	{
		if holdingObsidian == 0
		{
			"\![raise,OnDialogueVars,holdObsi]\![reset,sticky-window]\![set,sticky-window,0,1]\1\s[-1]\0%(wdc)"
			--
			"\s[630]%(wdn)HELLO,\w4 OBSIDIAN.%(reg)"
			"\s[630]%(wdn)HELLO,\w4 SWEET GIRL.%(reg)"
			"\s[630]%(wdn)STAYING WITH ME FOR A WHILE?%(reg)"
			"\s[600]%(t)Ah##\w4 alright##\w4 I'll hold her for a bit.]"
			"\s[600]%(t)Ok##\w4 I can hold her for a bit.]"
			"\s[606]%(t)Ah##\w4 she's not in your way##\w4 is she?]"
			--
			"\w8\w8\1%(holdnoise)"
		}
	}
}

MikireTalk
{
	if holdingObsidian == 1
	{
		"\0\s[610]%(wdc)%(t)Ah##\w4 %(username)?\w8\w8\s[606\] We're off the edge of the screen.]\w8\w8\n\n%(rg)* (You can press ctrl shift f12 to bring him back.)\w8\w8\1%(holdnoise)\e"
	}
	else
	{
		"\0\s[110]%(wdc)%(t)Ah##\w4 %(username)?\w8\w8\s[106\] I'm off the edge of the screen.]\w8\w8\n\n%(rg)* (You can press ctrl shift f12 to bring him back.)\w8\w8\1%(annoyednoises).\e"
	}
}

EndMikireTalk
{
	if holdingObsidian == 1
	{
		"\0\s[606]%(wdc)%(t)Thank you##]\w4 %(username).\1%(holdnoise)\e"
	}
	else
	{
		"\0\s[106]%(wdc)%(t)Thank you##]\w4 %(username).\1%(catnoises).\e"
	}
}

OnSurfaceRestore
{
	if holdingObsidian == 1
	{
		if RAND(99) < 59
		{
			"\1\s[-1]\0\s[600]"
		}
		else
		{
			holdingObsidian = 0
			jumpMenu = 0
			"\![get,property,OnObsidianJumpDown,currentghost.scope(0).rect]"
		}
	}
	else //Normal
	{
		"\0\s[0]\1%(catpose)\e"
	}
}

OnObsidianJumpDown //Obsidian jumping out of Dusty's arms
{
	_XCoord = TOINT(reference0[0])
	_XCoord = _XCoord - 20
	_YCoord = TOINT(reference0[3])
	_YCoord = (_YCoord - 120) //Go up by half Obsi's frame height
	
	"\![reset,sticky-window]\0\s[490]\w4\1\![set,scaling,-100,100]\s[1003]\![move,%(_XCoord),%(_YCoord),0]\![move,--X=-100,--Y=fix,--time=700,--base=0]\![set,scaling,100,100]\s[1004]%(holdnoise)\w8\w8\w8\0\s[400]"
	--
	"%(wdn)ALRIGHT THEN."
	"%(wdn)OK."
	"%(wdn)UP TO YOU."
	"%(wdn)TOO MUCH FOR NOW?"
	--
	if jumpMenu == 0 //If you were trying to open the menu
	{
		"\e"
	}
	else
	{
		jumpMenu = 0
		holdingObsidian = 0
		"\w8\w8%(reg)\n\n"
		--
		"\s[106]%(t)Alright##\w4 what did you need?]"
		"\s[106]%(t)Sorry about that##\w4 what did you need?]"
		--
		MenuRun
	}
}

OnKeyPress
{
	if reference0 == "t"
	{
		OnAiTalk
	}
	elseif reference0 == "r"
	{
		OnLastTalk
	}
	elseif reference0 == "f1"
	{
		"\![open,readme]"
	}
	elseif reference0 == "v" && debug == 1
	{
		OnTestVariable
	}
}