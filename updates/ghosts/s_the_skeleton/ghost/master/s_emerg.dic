OnSecondChange
{
	if !BalloonCheck
	{
		// _output = "\b2\_q"
		// foreach GETERRORLOG; _error
		// {
			// _output += "%(REPLACE(_error,'\','\\'))\n\n[half]"
		// }
		// _output += "\x"
		// _output
		_errors = ""
		
		_errorlist = REVERSE(GETERRORLOG)
		
		foreach _errorlist; _error
		{
			_error = SPLIT(_error,":")
			_file = SPLITPATH(_error[1])[2] + SPLITPATH(_error[1])[3]
			_file = SPLIT(_file,"(")
			_line = CUTSPACE(REPLACE(_file[1],")",""))
			_file = _file[0]
			_msg = "\_?" + CUTSPACE(_error[3]) + " " + CUTSPACE(_error[4]) + "\_?"
			
			_level = ""
			if "note" _in_ _error[2]; _level = "note"
			elseif "warning" _in_ _error[2]; _level = "warning"
			else; _level = "error"
			
			_errors += "\f[bold,1]\f[name,tahoma]%(_level) in %(_file) on line %(_line):\f[default]\n%(_msg)\n\n"
		}
		
		_num = ARRAYSIZE(GETERRORLOG)
		_s = ""; if _num != 1; _s = "s"
		_have = "have"; if _num == 1; _have = "has"
		
		"\0\s[0]\b2\_q\![set,autoscroll,disable]%(_num) error%(_s) %(_have) occured."
		--
		if _num >= 20; " Check Tama for the full error log."
		--
		"\n\n"
		--
		_errors
		--
		"\x"
	}
}

OnKeyPress
{
	if reference0 == "e"
	{
		"\b2\_q\_?%(GETERRORLOG)\_?\x"
	}
}

BalloonCheck
{
	if ASEARCH("Status",var.req.key) != -1
	{
		_currentstatus = var.req.value[ASEARCH("Status",var.req.key)]
		if "balloon" _in_ _currentstatus
		{
			1
		}
		else
		{
			0
		}
	}
	else; 0
}

TamaOpenDialogues : nonoverlap
{
	"\0\s[107]Yeah,\w4 you better open Tama."
}

TamaCloseDialogues
{
	"\0\s[106]Um,\w4 I dunno if you noticed,\w4 but there's still an error?"
}

On_tamaOpen
{
	TamaHWND = reference0
	SETTAMAHWND(TamaHWND)
	SHIORI3FW.Push_X_SSTP_PassThru('Title',"S the (broken) Skeleton") //These settings are all optional
	SHIORI3FW.Push_X_SSTP_PassThru('border.color','232323')
	SHIORI3FW.Push_X_SSTP_PassThru('background.color','F4F4F4')
	SHIORI3FW.Push_X_SSTP_PassThru('default.color','222222') //and any other settings you can find in tama.txt
	SHIORI3FW.Push_X_SSTP_PassThru('fatal.color','f23e3e')
	SHIORI3FW.Push_X_SSTP_PassThru('error.color','f23e3e')
	SHIORI3FW.Push_X_SSTP_PassThru('warning.color','ff972d')
	TamaOpenDialogues
}

On_tamaExit
{
	SETTAMAHWND(0)
	TamaCloseDialogues
}

OnInitialize
{
	if reference0 == "reload" && TamaHWND != ""; SETTAMAHWND(TamaHWND)
	else; TamaHWND = ""
}

OnDestroy
{
	if reference0 != "reload"; TamaHWND = ""
	SETTAMAHWND(0)
}
