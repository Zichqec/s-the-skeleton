//---------------------------Booting and Ending--------------------------
OnFirstBoot
{
	FirstVars
	if reference0 == 0 //Don't touch this line.
 	{
		"\0\s[-1]\![enter,passivemode]\0\s[1110]\w8Wh- \w8\w8 ...Oh,\w4 another timeline..?\w8\w8\n\n\s[110]Oh,\w4\s[1020] uh,\w4 hi there.\w8\w8\s[106] Sorry,\w4 I hope I'm not bothering you.\w8\w8\s[107] I have a tendency to fall into random timelines,\w4 I'll probably disappear again soon.\w8\w8\s[208]\n\nLooks like...\w8\w8 a computer this time?\w8\w8\s[107] Could be worse,\w4 I guess.\w8\w8\n\n\s[104]So ah,\w4 my name's S.\w8\w8\s[0] What's yours?\![leave,passivemode]\![open,inputbox,OnNameTeach,-1]\e"
 	}
	else
 	{
		reset += 1
 		"\![enter,passivemode]\0\s[1110]\w8Wh- \w8\w8 ...Oh,\w4 another timeline..?\w8\w8\n\n\s[1070].\w8\w8.\w8\w8.\w8\w8Or...\w8\w8 have I been here before..?\w8\w8\n\n\s[110]Oh,\w4\s[1020] uh,\w4 hi there.\w8\w8\s[106] Sorry,\w4 I hope I'm not bothering you.\w8\w8\s[107] I have a tendency to fall into random timelines,\w4 I'll probably disappear again soon.\w8\w8\s[208]\n\nLooks like...\w8\w8 a computer this time?\w8\w8\s[1070] Hmm...\w8\w8\n\n\s[104]Anyways,\w4 ah,\w4 my name's S.\w8\w8\s[0] What's yours?\![leave,passivemode]\![open,inputbox,OnNameTeach,-1]\e"
 	}
}


//Normal booting
OnBoot
{	
	BootVars
	_timesince = FUNCTIONEX("time_check.dll","how_many_days",lastclosed[0],lastclosed[1],lastclosed[2],0,0,0)//lastclosed[3],lastclosed[4],lastclosed[5] if you want the hours/minutes/seconds
	_timesince *= -1 //Multiply it by -1 to get rid of the negative sign, or to add one if the user traveled back in time.
	//The lastclosed array is set up in OnClose. Basically when you close him, it grabs the current time and saves it so that he can check it when he opens again.
	
	if nowmode == "RYS"
	{
		if RYSprogress == 5
		{
			RYSprogress = 6
			nowmode = "Default"
			"\1\s[-1]\0\s[1122]\w6Hi,\w4 %(username).\w8\w8\s[104] How have you been?\w8\w8\n\n* (He's back to normal...)\w8\w8\n\n* (What happened to %(RYS)..?)\e"
		}
		else
		{
			"\1\s[-1]\0\s[-1]\RYS\s[2790]\w8\w2"
		}
	}
	else
	{
		"\0\s[0]\1\s[-1]\0"
	}
	--
	//Hourcheck //[Zi]I don't know if this'll work.... but uh, I'm just gonna hope for the best //Later edit why did I do this? This does nothing
	dayslot = GetDaySlot
	timeslot = GetTimeSlot
	
	if nowmode == "RYS"
	{
		""
	}
	else
	{
		if LV == 0
		{
			
			if (dayslot == "8month 25day" || ((hour == 8 || hour == 20) && minute == 25)) && LVCheck == 1 && RYSprogress == 0 //If it's August 25th, or if the time is 8:25 AM or PM
			{
				if RYSprogress == 0
				{
					nowmode = "RYS"
					RYSprogress = 1
					RYSboots = 1
					RYSunlockDate = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
					"\0\s[-1]\RYS\s[2790].\w8.\w8.\w8.\w8\s[2015]!\w8\w8\w8\n\n\s[2004]* (This isn't S...\w8\w8 is it?)\w8\w8\w8\n\n\s[2007]* (He isn't saying anything...)\e"
				}
				else
				{
					"\0\s[1122]\w6\s[103]Hey there,\w4 %(username).\w8\s[1010] Did you call me here to wish me a happy birthday?\e"
				}
			}
			elseif _timesince < 0
			{
				"\0\s[1122]\w6\s[110]...%(username)?\w8\w8\s[1070] Uh...\w8\w8\s[106] Did you invent a time machine?\w8\w8\s[101] Or have you been messing with your clock?\e"
			}
			elseif dayslot == userbirthday
			{
				"\0\s[1122]\w6\s[1010]Happy birthday,\w4 %(username)!\w8\w8\n\n* (He hands you a picturesque slice of birthday cake.)\e"
			}
			elseif _timesince >= 365 //A year has passed
			{
				"\0\s[1122]\w6\s[110]Oh!\w8\w8\s[104] Hey there,\w4 %(username)!\w8\w8\s[111] It's been a long time,\w8\s[103] how've you been?\e"
				"\0\s[1122]\w6\s[110]Oh!\w8\w8\s[104] Hey,\w4 %(username)!\w8\w8\s[111] I haven't seen you in,\w4 what is it,\w4 at least a year?\w8\w8\s[103] How've you been?\e"
			}
			elseif _timesince >= 60 //At least a few months have passed
			{
				"\0\s[1122]\w6\s[110]Oh!\w8\w8\s[103] Hi,\w4 %(username).\w8\w8\s[104] It's been a few months,\w4 how are things going?\e"
				"\0\s[1122]\w6\s[110]Oh!\w8\w8\s[103] Hey,\w4 %(username).\w8\w8\s[104] I haven't seen you in a while,\w4 how have you been?\e"
			}
			elseif dayslot == "7month 4day"
			{
				"\0\s[1122]\w6\s[106]It's that time of year again...\w8\w8\s[200] All the humans in our area like to set off colorful explosions in the sky.\w8\s[201] I hear it's some sort of celebration of freedom from a long time ago?\w8\w8\n\n\s[1070]Maybe monsters should have a celebration of our own freedom...\w8\w8\s[202] Could do without the explosions though.\w8\s[206] Maybe some sort of colorful magic displays instead..?\e"
			}
			elseif dayslot == "4month 1day"
			{
				"\0\s[1122]\w6\s[110]Oh-!\w8\w8\s[107] Hey there,\w4 %(username).\w8\w8\s[106] You're not going to play any pranks on me,\w4 are you?\e"
			}
			elseif lastclosed[0] != year //If the year has rolled over since you last saw him, but it also hasn't been too long since he last saw you. This also restricts him to commenting on the year to january and february, which feels about right.
			{
				"\0\s[1122]\w6\s[104]Happy new year,\w4 %(username)!\w8\w8\s[103] Are you excited for %(year)?\w8\w8\s[205] I am,\w4 there's a lot I hope to get done.\e"
			}
			elseif day == 1
			{
				"\0\s[1122]\w6\s[104]Happy %(displaymonth),\w4 %(username)!\e"
				"\0\s[1122]\w6\s[103]Hey %(username),\w4\s[104] happy %(displaymonth)!\e"
			}
			elseif "Dusty" _in_ ghostexlist && talkedtoDusty == 1
			{
				res_reference0 = "Dusty"
				"\0\s[1122]\w6\s[104]Hey there,\w4 %(username).\w8\w8\s[205] Hey,\w4 Dusty.\e SBootDusty1"
			}
			//Note to self for later, I don't feel like adding these now but I probably will later: Triplets Bday 10/12 (Barcode should have a different one... I'll have to pick an arbitrary one), Blue Jeans 8/11, Rosie 4/27, Roman 12/13, Cambria 3/10, Triton 3/23, Lulo 2/26, Azo 3/13, Sigil is technically 9/28... but I may need to bump that around to match with Azo. Or bump Azo around. One of those... Sigil should be 9-10 months later. Elko 2/26. Maaan these are like all clumped up around the same months XD Do I want to spread them out? Mm.... I'll decide later.
			/*
			1/28 //Sigil? Maybe. Or this could be Azo and I could leave Sigil where she's at..... That's probably better
			2/19
			2/26
			3/10
			3/13
			3/23
			4/27
			8/11
			8/25
			10/12
			12/13
			*/
		}
	}
	--
	normalboottalk
	
}


normalboottalk //Ergh, this is a mess. For now, he'll only acknowledge the time if you're being nice to him. Might change later.
{
	if nowmode == "RYS"
	{
		if RYStrust >= 100 && RYSlearn[1] >= 3
		{
			"\RYS\s[2006]* (He doesn't say anything,\w4 but you think you see a slightly hopeful look cross his face.)\e"
			"\RYS\s[2006]* (He gives you the tiniest of waves.)\e"
			"\RYS\s[2006]* (He looks glad to see you,\w4 you think.)\e"
		}
		else
		{
			"\RYS\s[2006]* (He glances at you,\w4 but stays quiet.)\e"
			"\RYS\s[2006]* (He stands meekly,\w4 waiting.)\e"
			"\RYS\s[2007]* (He shifts nervously as he appears.)\e"
		}
	}
	else
	{
		if LV == 0 && love >= 20
		{
			{
				"\0\s[1122]\w6\s[103]Heya,\w4 %(username).\w8 It's good to see you again.\e"
				"\0\s[1122]\w6\s[0]Heya.\w8 How's it going?\e"
				"\0\s[1122]\w6\s[104]Hey there %(username).\w8\w8\s[103] How's your day been?\e"
				"\0\s[1122]\w6\s[104]Hey %(username),\w4 having a good %(displayweekday)?\e"
				"\0\s[1122]\w6\s[104]Hey %(username),\w4 how's it going?\e"
				"\0\s[1122]\w6\s[103]Good to see you,\w4 %(username).\w8\w8\s[104] How's your %(displayweekday) going?\e"
				
				{
					"\s[1110]\w6"
					--
					"And so-"
					"That's because-"
					"Well if we-"
					"I can't-"
					"Have you-"
					"I know-"
					"Maybe I-"
					"How about-"
					--
					"\n\n\w8\w8\s[110]Oh,\w8\s[104] hey there %(username).\w8\w8\s[0] "
					"\n\n\w8\w8\s[110]Oh,\w8\s[104] hi %(username).\w8\w8\s[0] "
					"\n\n\w8\w8\s[110]Oh,\w8\s[104] good to see you %(username).\w8\w8\s[0] "
					--
					"What's up?\e"
					"How's it going?\e"
					"How's your day been?\e"
				}
			}
			
			if nowshell == "pink_apron"
			{
				"\0\s[1122]\w6\s[102]* (The smell of baked goods heralds his presence.)\w8\w8\n\n\s[110]Oh!\w8\s[104] Hey,\w4 %(username).\w8\w8\s[103] You caught me in the middle of making %(bakedgoods).\e"
			}
			
			//---Timeslots---
			if timeslot == "earlymorning" //5-8
			{
				"\0\s[1122]\w6\s[104]Heya,\w4 %(username).\w8 Are you an early waker too?\w8\w8\s[101] Or did you stay up all night?\e"
				"\0\s[1122]\w6\s[103]Good morning,\w4 %(username)!\w8\w8\w8\w8\n\n\s[104]* (He starts humming a chipper tune.)\w8\w8\n\n* (Yep,\w4 definitely a morning person.)\e"
			}
			if timeslot == "morning" //9-11
			{
				"\0\s[1122]\w6\s[103]Hi,\w4 %(username).\w8 How's your morning going?\e"
				"\0\s[1122]\w6\s[0]Hey %(username).\w8\w8\s[104] Happy %(displayweekday).\e"
				"\0\s[1122]\w6\s[103]Good morning,\w4 %(username)!\e"
				if nowshell == "pink_apron"
				{
					"\0\s[1122]\w6\s[102]* (You can smell pancakes and syrup.)\w8\w8\n\n\s[110]Oh!\w8\w8\s[104] Hey there %(username),\w4 we were just finishing up breakfast.\w8\w8\s[0] Have you had anything to eat yet?\e"
				}
			}
			if timeslot == "lunch" //12-2
			{
				"\0\s[1122]\w6\s[103]Hey there,\w4 %(username).\w8 Have you eaten yet?\e"
				if hour > 12
				{
					"\0\s[104]Good afternoon,\w4 %(username).\e"
				}
				if nowshell == "pink_apron"
				{
					"\0\s[1122]\w6\s[102]* (The scent of %(ingredients) drifts through your desktop.)\w8\w8\s[110]\n\nOh,\w4\s[104] hey there %(username).\w8\w8\s[0] I was just making lunch,\w4 have you had lunch yet?\e"
				}
			}
			if timeslot == "afternoon" //3-5
			{
				"\0\s[1110]\w6\s[207]* (He yawns.)\n\n\w8\w8\s[106]Oh,\w4 hi there %(username).\w8\s[107] Sorry,\w4 I was just about to take a nap.\e"
				"\0\s[1122]\w6\s[110]Oh,\w4\s[104] afternoon,\w4 %(username).\e"
				"\0\s[1127]\w6\s[1300].\w2.\w2.\w2%(mumble)...\w8\w8\n\n* (Looks like he's taking a nap...)\w8\w8\n\n* (Do you want to send him home?)\n\n\![*]\q[Keep him here,bootkeep]\n\![*]\q[Send him home,boothome]\*\t\e:eval=nowmode='Asleep'"
			}
			if timeslot == "evening" //6-8
			{
				"\0\s[1110]\w6\s[207]* (He yawns.)\n\n\w8\w8\s[0]Oh,\w4 hey there %(username).\w8 Is it getting late where you're at?\w8\s[107] The sun's already set at home.\e"
				"\0\s[1122]\w6\s[104]Hey,\w4 %(username).\w8\w8\s[0] Spending a relaxing %(displayweekday) evening at home?\e"
			}
			if timeslot == "latenight" //9-12
			{
				"\0\s[1122]\w6\s[109]* (He seems confused for a moment.)\n\n\w8\w8\s[1100]Oh..!\w8\s[107] Hey,\w4 %(username).\w8\s[101] Isn't it late there..?\w8\w8\n\n\s[207]* (He yawns slowly.)\w8\w8\w8\n\n\s[107]I was just on my way to bed,\s[0] but I'm here if you need me.\e" //replaceframe could use sleepy with eyes open
				"\0\s[1122]\w6\s[110]Ah-\w8\w8\s[106] Oh,\w4 hey %(username).\w8\w8\s[104] Planning to stay up a while?\e"
				"\0\s[1127]\w6\s[1300].\w2.\w2.\w2%(mumble)...\w8\w8\n\n* (Looks like he's already asleep...)\w8\w8\n\n* (Do you want to send him home?)\n\n\![*]\q[Keep him here,bootkeep]\n\![*]\q[Send him home,boothome]\*\t\e:eval=nowmode='Asleep'"
			}
			if timeslot == "midnight" //1-4
			{
				"\0\s[1122]\w6\s[108]Ah...\w8\w8 Hey there,\w4 %(username).\w8\s[303] You can't sleep either?\e"
				"\0\s[1122]\w6\s[109]Hm?\w8\w8\s[110] Ah-\w8\s[104] hey %(username).\w8\w8\s[101] You're up late.\e"
				"\0\s[1122]\w6\s[109]...Hm?\w8\w8\s[108] Oh-\w8\s[109] Hey %(username).\w8\w8\w8\n\n\s[204]Mmm...\w8\w8\s[202] I'm glad you've brought me here,\w4 because I wasn't getting any sleep at home.\e"
				"\0\s[1127]\w6\s[1300].\w2.\w2.\w2%(mumble)...\w8\w8\n\n* (Looks like he's asleep...)\w8\w8\n\n* (Do you want to send him home?)\n\n\![*]\q[Keep him here,bootkeep]\n\![*]\q[Send him home,boothome]\*\t\e:eval=nowmode='Asleep'"
				"\0\s[1127]\w6\s[1300].\w2.\w2.\w2%(mumble)...\w8\w8\n\n* (Looks like he's asleep...)\w8\w8\n\n\s[109]Bh-\w8\s[108] Hm?\w8\w8\s[110] Oh...\w8\w8\s[106] Hey,\w4 %(username).\w8\w8\s[206] Uh,\w4 when did I end up here..?\w8\w8\s[107] Well,\w4 alright,\w4 I'm up anyways.\e"
			}
		}
		elseif LV > 0 //Negative routes
		{
			if LVCheck == 1 //know his secrets
			{
		
				if broken == 1 //broken him down all the way
				{
					"\s[1126]\w6"
					--
					"\s[510]* (He doesn't look up at you.)\e"
					"\s[510]* (He nods in acknowledgement,\w4 but says nothing.)\e"
					"\s[510]* (He shudders.)\e"
					"\s[510]* (He emerges from within your files.)\w8\w8\n\n* (Seems like he didn't go home last time.)\e"
				}
				else //breaking him, but he's still standing
				{
					"\0\s[1122]\w6\s[108]...Oh.\w8\w8\s[510] Here again...\e"
				}
			}
			else //Just mean for no reason
			{
				if LV <= 30 //you've been a bit mean
				{
					"\s[1124]\w6\s[414]Oh,\w4 great.\w8\w8 \s[412]You again?\e"
					"\s[1123]\w6\s[404]Ugh,\w4 not this again.\e"
					"\s[1123]\w6\s[403]What do you want this time?\e"
				}
				elseif LV >=31 //You've been really mean and he hates you a lot
				{
					"\s[1123]\w6\s[403]What,\w4 still not tired of punching me?\e"
					"\s[1124]\w6\s[411]I'm beginning to think you have nothing better to do with your time.\e"
					"\s[1124]\w6\s[412]Get a life,\w4 seriously.\e"
					"\s[1123]\w6\s[404]* (He looks away and doesn't say anything.)\e"
				}
			}
		}
		else //Neutral has to be down here and not up with the positive ones, unless I change it so he recognizes the time
		{
			"\0\s[1122]\w6"
			--
			"\0\s[110]Oh,\w4 here again?\w8\w8\s[205] Alright then.\e"
			"\0\s[101]Oh,\w4 hello again.\w8\w8\s[205] Didn't expect to come back here.\e"		
		}
	}
}

Select.bootkeep
{
	"\0\s[1300]* (He can sleep here for a while.)\e"
}

Select.boothome
{
	"\0\s[1300]* (He'll probably sleep better at home.)\w8\w8\n\n* (Maybe he'll be awake later...)\-\e"
}

OnClose
{
	lastclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
	timeslot = GetTimeSlot //as you saw above, it's checking the time.
	--
	if passmin >= 5 || passhour > 0 //This checks to see if you've had them open for at least five minutes.
	{
		if nowmode == "RYS"
		{
			if RYSprogress == 4
			{
				RYSprogress = 5
			}
			--
			if RYStrust > 100
			{
				"\RYS\s[2007]Oh,\w4 um...\w8\w8\s[2006] Alright.\w8\w8 See you later..?"
				"\RYS\s[2008]Oh,\w4 ok...\w8\w8\n\n\s[2006]* (You get the feeling he wants to say something,\w8\s[2007] but then he thinks better of it and \s[-1]leaves quickly.)"
			}
			else
			{
				"\RYS\s[2007]* (He nods and \s[-1]leaves quietly.)"
				"\RYS\s[2006]* (He nods and \s[-1]leaves quickly.)"
				"\RYS\s[2006]* (He nods and \s[-1]is quickly gone.)"
				"\RYS\s[2006]* (He nods and \s[-1]makes himself scarce.)"
			}
		}
		elseif holdingObsidian == 1
		{
			"\0\s[1206]Oh,\w4 alright then.\w8\w8\s[1201] Guess I gotta give you back to Dusty,\w4 huh?\w8\w8\s[1204] See you guys around."
		}
		elseif nowmode == "Asleep"
		{
			"\0* (He'll probably sleep better at home...)\w8\w8\n\n...%(mumble)..."
			"\0* (He must be exhausted...)\w8\w8\n\n* (He should rest easier at home.)\w8\w8\n\n...%(mumble)..."
			"\0* (He must be tired if he's fallen asleep here...)\w8\w8\n\n* (Better to send him home so his family can take care of him.)\w8\w8\n\n...%(mumble)..."
		}
		else
		{
			if LV == 0 && love >= 20
			{
				if RAND(100) < 40
				{
					"\0\s[1010]See you later,\w4 %(username)!"
					"\0\s[104]Take care,\w4 %(username)!"
					"\0\s[102]See you around,\w4 %(username)."
					if hour <= 18
					{
						"\0\s[104]Have a good day,\w4 %(username)!"
						"\0\s[103]Have a good %(displayweekday),\w4 %(username)!"
					}
					if hour > 18
					{
						"\0\s[104]Have a good night,\w4 %(username)!"
						"\0\s[103]Have a good evening,\w4 %(username)!"
					}
				}
				elseif wd == "Sat" && RAND(100) < 60
				{
					"\0\s[1010]Enjoy the weekend,\w4 %(username)!"
					"\0\s[107]Heh,\w4 back to the weekend chaos for me.\w8\w8\s[103] Have a good day,\w4 %(username)."
				}
				else
				{
					if timeslot == "earlymorning"
					{
						if passhour < 3 //Earlymorning is from 5-8, so if it's been less than 3 hours then the user's probably a super early riser. Probably.
						{
							"\0\s[0]Off to get breakfast?\w8\s[1010] Fair enough.\w8\w8\s[101] Or maybe you're just going to bed.\w8\w8\s[1010] Either way,\w4 I'll see you later."
						}
						else //If it's been more than 3 hours they've been awake since at least the midnight timeslot, so probably haven't been to bed.
						{
							"\0\s[106]Finally off to bed,\w4 huh?\w8\w8\s[107] As long as you're taking care of yourself,\w4 I guess.\w8\w8\s[104] Sleep well,\w4 %(username)."
						}
					}
					elseif timeslot == "morning"
					{
						"\0\s[1010]There's a lot of the day still left.\w8 Make it a good one,\w4 yeah?"
					}
					elseif timeslot == "lunch"
					{
						"\0\s[106]Good timing,\w4 I'm starting to get hungry.\w8\s[1010] Don't forget to eat something yourself,\w4 ok?"
						"\0\s[0]Off to eat something?\w8\w8\s[104] Me too,\w4 see you later."
					}
					elseif timeslot == "afternoon"
					{
						"\0\s[205]I should go pick up the kids.\w8\w8\s[1010] See you later,\w4 %(username)!"
						"\0\s[205]Good timing,\w4 I should have a chance to swing by the store before they close.\w8\w8\s[104] See you later,\w4 %(username)."
					}
					elseif timeslot == "evening"
					{
						"\0\s[0]Alright then,\s[1010] I'm off to see what's for dinner.\w8\s[0] Don't forget to eat something yourself,\w4 too."
					}
					elseif timeslot == "latenight"
					{		
						"\0\s[1010]Goodnight,\w4 %(username).\w8 Sleep well."
						"\0\s[207]* (He yawns.)\w8\w8\w8\n\n\s[106]Guess it's time for me to head to bed.\w8\w8\s[104] You make sure you get some sleep too,\w4 ok %(username)?"
					}
					else //Midnight
					{
						"\0\s[200]Guess it's time to try and get some sleep.\w8\s[202] I hope you have better luck than me,\w4 %(username)."
						"\0\s[207]* (He yawns.)\w8\w8\w8\n\n\s[107]I dunno about you,\w4 but I can't wait to crawl under a warm blanket.\w8\w8\s[104] You get some sleep too,\w4 ok %(username)?"
					}
				}
			}
			elseif LV > 0 //Negative routes
			{
				if LVCheck == 1 //know his secrets
				{
			
					if broken == 1 //broken him down all the way
					{
						"\s[514]* (You tell him to crawl back to his family.)\w8\w8\n\n\s[500]* (He nods and\s[-1] slinks away.)"
						"\s[510]* (You tell him to get lost.)\w8\w8\n\n\s[-1]* (He does so without a word.)"
						"\s[1550]* (You shove him out,\w8\s[-1] and he puts up no resistance.)"
					}
					else //breaking him, but he's still standing
					{
						"\0\s[510]* (You tell him to go back to his family.)\w8\w8\n\n\s[516]* (He gives you a wary look,\w4\s[500] then nods.)"
						"\0\s[516].\w8.\w8.\w8Thanks."
						"\0\s[516]* (He looks like he's going to say something,\w4\s[510] then changes his mind and\s[-1] leaves quietly.)"
					}
					
				}
				else //Just mean for no reason
				{
					if LV <= 30 //you've been a bit mean
					{
						"\s[400]Good riddance."
						"\s[400]Finally."
					}
					elseif LV >=31 //You've been really mean and he hates you a lot
					{
						"\s[400]Good riddance."
						"\s[400]Finally."
					}
				}
			}
			else //Neutral has to be down here and not up with the positive ones, unless I change it so he recognizes the time
			{
				"\0\s[0]Alright.\w8 See you around,\w4 then."
				"\0\s[0]Ah,\w4 alright.\w8 See you later."
			}
		}
	}
 	else //This dialogue is for if they haven't been open for at least five minutes.
 	{
		if nowmode == "RYS"
		{
			if RYSprogress == 4
			{
				RYSprogress = 5
			}
			--
			if RYStrust > 100
			{
				"\RYS\s[2006]Oh,\w4 already..?\w8\w8\s[2007] Ok..."
			}
			else
			{
				"\RYS* (He's\s[-1] gone before you have the chance to say anything else...)"
			}
		}
		elseif holdingObsidian == 1
		{
			"\0\s[1206]Oh,\w4 ok then.\w8\w8\s[1201] Well,\w4 at least I got to hold you while I was here.\w8\w8\s[1204] See you around,\w4 Dusty."
		}
		elseif nowmode == "Asleep"
		{
			"\0\s[1300]* (Actually,\w4 maybe he'll sleep better at home...)"
		}
		else
		{
			if LV == 0 //Friendship routes
			{
				if love >=100 //Full friendship
				{
					if hugged > 0
					{
						"\0\s[110]Oh,\w4 just wanted a hug?\w8\w8\s[104] Alright then,\w4 don't be afraid to call me again if you need me."
					}
					else
					{
						"\0\s[110]Oh,\w4 did you call me here by accident?\w8\s[1010] Alright,\w4 well,\w4 have a good day anyways!"
					}
					
				}
				elseif love >= 30 //getting to know him a little
				{
					"\0\s[106]Oh,\w4 alright then.\w8\w8\s[104] See you later."
				}
				else //He isn't sure what to make of you yet
				{
					"\0\s[108]Oh,\w4 uh,\w4 alright.\w8\w8\s[208] I've got some stuff I should do at home anyhow."
				}
			}
			
			elseif LV > 0 //Negative routes
			{
				if LVCheck == 1 //know his secrets
				{

						if broken == 1 //broken him down all the way
						{
							"\s[1550]* (You shove him out,\w8\s[-1] you didn't want him here anyways.)\w8\w8"
						}
						else //breaking him, but he's still standing
						{
							"\s[516]* (He leaves quickly,\w4\s[-1] seems he wasn't eager to stick around anyways.)"
						}
					
				}
				else //Just mean for no reason
				{
					if LV <= 30 //you've been a bit mean
					{
						"\s[401]Fine by me,\w4 I didn't want to be here anyways."
					}
					elseif LV >=31 //You've been really mean and he hates you a lot
					{
						"\s[412]Are you finally starting to get bored?\w8\w8\s[401] Good."
					}
				}
			}
		}
 	}
 	--
	"\w8\w8\-\e"
}


//-------------------------State Changing--------------------
//From here on, most of the dialogue will have to do with changing shells, ghosts, or states in some way.
 

//--OnWindowStateRestore

//This will run when you minimize them and then bring them back up. Replace with what you like.

OnWindowStateRestore
{
	lastinteracted = 0
	minimized++
	if nowmode == "RYS"
	{
		"\RYS\s[2014]* (He squirms and shivers.)\e"
	}
	elseif nowmode == "Asleep"
	{
		"\0\s[1302]...%(mumble)...\e"
	}
	else
	{
		if LV == 0 //Friendship routes - Any amount of friendship will trigger this dialogue
		{
			"\0\s[301]Ah..!\w8\n\n* (He's hyperventilating a little.)\n\n\w8\w8\w8\w8\s[200]Wh-what was that??\w8\w8\s[201] Uh,\w4 %(username),\w4 I'm uh-\w8\w8\s[300] I'm a bit scared of the dark.\w8 And uh...\w8\w8\s[301] Tight spaces.\w8\w8\n\n\s[303]Can we not do that again?\w8 Please?\e" //replaceframe MAYBE some fear frames
			"\0\s[301]* (He's shuddering violently.)\w8\w8\n\n* (...Maybe you should try not to minimize him.)\w8\w8\n\n\s[302]* (Deep breaths seem to be calming him,\w4 at least.)\e"
			"\0\s[301]* (His bones are rattling fearfully.)\w8\w8\n\n\s[206]Wh-what was that..?\w8\w8\s[201] %(username),\w4 can you not do that?\w8\w8\s[303] I don't do so well with cramped spaces...\w8\w8\s[300] Or uh,\w4 dark spaces...\e"
		}
		else
		{
			if LVCheck == 1 //know his secrets
			{
				"\s[511]* (He's hyperventilating.)\w8\w8\n\n\s[515]* (Good.)\e"
			}
			else //Just mean for no reason
			{
				"\s[400]Tsk.\w8\w8\n\n* (He's acting like that didn't bother him,\w4 but you can see his shoulders trembling a little.\e"
			}
		}
	}
}


//--OnGhostChanging

 
OnGhostChanging
{
	if reference0 == "RYS-0825"
	{
		if nowmode == "RYS"
		{
			"\RYS\s[2006]Um,\w4 alright,\w4 I'll reload..."
		}
		elseif holdingObsidian == 1
		{
			"\0\s[1201]Ok,\w4 I'll reload."
		}
		elseif nowmode == "Asleep"
		{
			"\0* (Reloading...)"
		}
		else
		{
			if LV == 0 //Friendship routes
			{
				"\0\s[1010]Sure, be back in just a moment."
				"\0\s[0]Alright, just a moment, then."
				"\0\s[104]Alright,\w4 I'll reload."
			}
			elseif LV > 0 //Negative routes
			{
				if LVCheck == 1 //know his secrets
				{

						if broken == 1 //broken him down all the way
						{
							"\s[516]* (You order him to reload.)\w8\w8\s[510]\n\n* (He simply nods and complies.)"
						}
						else //breaking him, but he's still standing
						{
							"\s[516]...Ok, I'll reload."
						}
					
				}
				else //Just mean for no reason
				{
					"\0\s[400]Only because it gets me away from you briefly."
				}
			}
		}
		--
		"\w8\e"
	}
 	else //If you're switching to another Ghost.
	{
		lastclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
		if nowmode == "RYS"
		{
			if RYSprogress == 4
			{
				RYSprogress = 5
			}
			"\RYS\s[2006]Alright,\w4 I'll fetch %(reference0)..."
		}
		elseif holdingObsidian == 1
		{
			"\0\s[1210]Oh,\w4 ok then.\w8\w8\s[1201] Guess I've got to put you down.\w8\w8\s[1204] See you around,\w4 Dusty."
		}
		elseif nowmode == "Asleep"
		{
			"\0* (Maybe %(reference0) will be awake...)\w8\w8\n\n* (S looks like he could use a rest at home.)"
		}
		else
		{
			if LV == 0 //Friendship routes
			{
				if love <= 19 //He isn't sure what to make of you yet
				{
					"\0\s[0]Ok. Say hi to %(reference0) for me!"
				}
				else
				{
					"\0\s[1010]Ok. Say hi to %(reference0) for me!"
				}
			}
			elseif LV > 0 //Negative routes
			{
				if LVCheck == 1 //know his secrets
				{

						if broken == 1 //broken him down all the way
						{
							"\s[516]* (You order him to go and fetch %(reference0) for you.)\w8\w8\s[510]\n\n* (He nods and hurries off.)"
						}
						else //breaking him, but he's still standing
						{
							"\0\s[510]...Ok."
						}
					
				}
				else //Just mean for no reason
				{
					"\0\s[402]Well,\w4 I hope you at least treat %(reference0) better than you've treated me."
				}
			}
		}
		--
		"\w8\w8\e"
	}
}


//--OnGhostChanged

//A sister to the above, this runs after the ghost has either reloaded itself, or when you switch from another ghost to this ghost.

OnGhostChanged
{
	if nowmode == "RYS"
	{
		if RYSprogress == 5
		{
			RYSprogress = 6
			nowmode = "Default"
			"\1\s[-1]\0\s[1122]\w6Hi,\w4 %(username).\w8\w8\s[104] How have you been?\w8\w8\n\n* (He's back to normal...)\w8\w8\n\n* (What happened to %(RYS)..?)\e"
		}
		else
		{
			"\0\s[-1]\1\s[-1]\RYS\s[2006]"
		}
	}
	elseif holdingObsidian == 1
	{
		"\0\s[1201]"
	}
	elseif nowmode == "Asleep"
	{
		"\0\s[1300]"
	}
	else
	{
		"\0\s[0]\1\s[-1]" //this sets up their default poses
	}
	--
	if reference0 == "RYS-0825" //As above, this should be the name of your ghost as in install.txt or descript.txt.
	{
		if nowmode == "RYS"
		{
			"\RYS\s[2006]I've finished.\e"
		}
		elseif holdingObsidian == 1
		{
			"\0\s[1201]Back.\e"
		}
		elseif nowmode == "Asleep"
		{
			"* (Done.)\e"
		}
		else
		{
			if LV == 0 //Friendship routes
			{
				"\0\s[1010]I'm back.\e"
				"\0\s[1010]Back.\w8\w8 Anything new?\e"
				if love <= 19 //He isn't sure what to make of you yet
				{
					"\0\s[0]I'm back.\e"
					"\0\s[0]Back.\w8\w8 Anything new?\e"
				}
			}
			elseif LV > 0 //Negative routes
			{
				if LVCheck == 1 //know his secrets
				{
					if broken == 1 //broken him down all the way
					{
						"\s[510]* (Reload complete.)\e"
					}
					else //breaking him, but he's still standing
					{
						"\s[510]* (Reload complete.)\e"
					}
				}
				else //Just mean for no reason
				{
					"\0\s[404]A shame that doesn't take longer.\e"
					"\0\s[-1]\_w[5000]\_w[5000]\_w[5000]\s[404]...You don't have to wait up for me,\w4 you know.\e"
				}
			}
		}
	}
	else //If you're switching to them from another ghost...
	{
		BootVars
		
		if nowmode == "RYS"
		{
			if RYStrust >= 100
			{
				"\RYS\s[2006]...There was someone leaving...\w8\w8\s[2007] Did you call them here,\w4 too..?\e"
			}
			else
			{
				"\RYS\s[2007]* (He doesn't comment about %(reference0)...)\w8\w8\s[2006]\n\n* (Perhaps he avoided them?)\e"
			}
		}
		else
		{
			if LV == 0 //Friendship routes
			{
				if (dayslot == "8month 25day" || ((hour == 8 || hour == 20) && minute == 25)) && LVCheck == 1 && RYSprogress == 0 //If it's August 25th, or if the time is 8:25 AM or PM
				{
					if RYSprogress == 0
					{
						nowmode = "RYS"
						RYSprogress = 1
						RYSboots = 1
						RYSunlockDate = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
						"\0\s[-1]\RYS\s[2790].\w8.\w8.\w8.\w8\s[2015]!\w8\w8\w8\n\n\s[2004]* (This isn't S...\w8\w8 is it?)\w8\w8\w8\n\n\s[2007]* (He isn't saying anything...)\e"
					}
					else
					{
						"\0\s[1122]\w6\s[103]Hey there,\w4 %(username).\w8\s[1010] Did you call me here to wish me a happy birthday?\e"
					}
				}
				else
				{
					"\0\s[1122]\w6\s[110]Was that %(reference0) leaving?\e"
				}
			}
			elseif LV > 0 //Negative routes
			{
				if LVCheck == 1 //know his secrets
				{
					"\s[516]* (You ask if he saw how you treated %(reference0).)\w8\w8\n\n\s[510]* (He just shivers.)\e"
				}
				else //Just mean for no reason
				{
					"\0\s[402]%(reference0),\w4 huh?\w8\w8\s[400] Did you punch them too?\e"
				}
			}
		}
	}
}


//--OnShellChanging

//This will run when you change your ghost's shell. If you don't have any other shells for your ghost, you can safely ignore this but don't delete it, you might as well keep it just in case. I probably won't add a different shell to this template because shells are a lot of work ugh, but for the sake of argument, let's pretend I did.

OnShellChanging
{
	"\![get,property,OnGetCoords,currentghost.scope(0).rect]" //This will get his coordinates on the screen and put them into the function below, so that when he changes shells he can move to the same spot... Thanks again to Levidre for helping me with this
	--
	if nowmode == "RYS"
	{
		"\RYS\s[2006]* (He shuffles away\s[-1] obediently to change clothing.)\w8\e"
	}
	elseif holdingObsidian == 1
	{
		"\0\s[1201]Ok,\w4 I'll be right back.\w8\e"
	}
	elseif nowmode == "Asleep"
	{
		"%(etcwake)Uh,\w4 yeah sure,\w4 hold on...\w8\e"
	}
	else
	{
		if LV == 0
		{
			_tempshell = reference0 //storing the shell name in _tempshell
			_tempshell = TOLOWER(_tempshell) //changes it to lowercase
			_tempshell = REPLACE(_tempshell," ","_") //replace spaces with underscores. This should work as long as my naming schemes never change |D
			if _tempshell == nowshell //[Zi]If you're switching to the same shell... for some reason. I don't know why it lets you do this but I mean hey it means I get to add a silly comment yeah? I wont REALLY know if this code works until I add a second shell.... but I'm hoping that'll be sooner over later :3c
			{
				"\0\s[106]Uh...\w8\w8 I'm already wearing that,\w4 but ok I guess?\w8\e"
			}
	/*		elseif reference0 == "red_hoodie"
			{
			""
			}*/ //[Zi]I can't think of anything to have him say about his hoodie at the moment..... so I'm just going to comment this out. If I think of something later I'll add it back in.
			elseif reference0 == "Pink Apron"
			{
				"\0\s[208]Do you smell cookies?\w8\e"
			}
			elseif reference0 == "Autumn Flannel"
			{
				if season == "Autumn"
				{
					"\0\s[205]Hmm,\w4 it's getting cooler out...\w8\e"
				}
				else
				{
					"\0\s[106]Uh,\w4 alright,\w4 just a moment.\w8\e"
				}
			}
			else
			{
				"\0\s[103]Alright,\w4 I'll be right back.\w8\e"
			}  
		}
		else
		{
			if LVCheck == 1
			{
				"\0\s[510]* (He nods and goes to switch clothing.)\w8\e"
			}
			else
			{
				"\0\s[400]Hmph.\w8\w8 Fine.\w8\e"
			}
		}
	}
}
 
 
//--OnShellChanged

//The sister to the above, this is the dialogue after you've changed the shell.
 
OnShellChanged
{
	if booting != 2
	{
		tempshell = reference0 //storing the shell name in tempshell
		tempshell = TOLOWER(tempshell) //changes it to lowercase
		tempshell = REPLACE(tempshell," ","_") //replace spaces with underscores. This should work as long as my naming schemes never change |D
		if nowmode == "RYS"
		{

		}
		elseif holdingObsidian == 1
		{
			if tempshell == nowshell
			{
				"\0\s[1206]Was that really necessary,\w4 kiddo?\e"
			}
		}
		elseif nowmode == "Asleep"
		{
			"\0\s[1300]"
			--
			if tempshell == nowshell
			{
				"\0\s[1300]%(mumb)%(mumb)%(mumb)zz...\e"
			}
		}
		else
		{
			if LV == 0
			{
				if tempshell == nowshell
				{
					"\0\s[106]Was that really necessary,\w4 kiddo?\e"
				}
			}
		}
		switchingtoshell = reference0
		shellswitching = 1
		if switchingtoshell == "Red Hoodie" //Again, this is the name of your shell as you put in the descript.txt file for that shell.
		{
			nowshell = "red_hoodie"
		}
		elseif switchingtoshell == "Pink Apron"
		{
			nowshell = "pink_apron"
		}
		elseif switchingtoshell == "Autumn Flannel"
		{
			nowshell = "autumn_flannel"
		}
		else
		{
			nowshell = "Other"
		}
		--
		"\![move,--X=%(shellchangexcoord),--Y=%(shellchangeycoord),--base=primaryscreen]" //Here's the move command, this puts him to the spot he was before you switched shells
		--
		"\![change,balloon,%(nowballoon)]\![raise,OnShellChangeDialogue]"  //The balloon needs to switch when the shell does, because of the kero balloon choice markers. This bit of code makes the balloons be the same even if youre switching around shells, but it means the dialogue has to be put below in OnBalloonChange...
	}
	else
	{
		"\![raise,OnUpdateComplete]"
	}
}

OnGetCoords
{
	shellchangexcoord = reference0[0]
	shellchangeycoord = reference0[1]
}

OnBalloonChange
{
	nowballoon = reference0
	
	--
	if shellswitching == 1 //Here's the dialogue for when you've switched shells, so that it doesnt get interrupted.
	{
		"\![raise,OnShellChangeDialogue]"
	}
	
}

OnShellChangeDialogue
{
	shellswitching = 0
		if nowmode == "RYS"
		{
			"\RYS\s[2006]I've finished...\e"
		}
		elseif holdingObsidian == 1
		{
			"\0\s[1200]Done.\e"
		}
		elseif nowmode == "Asleep"
		{
			"\0\s[1300]%(mumb)%(mumb)%(mumb)zz...\e"
		}
		else
		{
			if LV == 0
			{
				if switchingtoshell == "Red Hoodie" //Again, this is the name of your shell as you put in the descript.txt file for that shell.
				{
					"\0\s[104]Ahh,\w4 this is always nice to change back to.\w8\w8\s[0] It's simple,\w4\s[103] but it's comfy.\e"
				}
				elseif switchingtoshell == "Pink Apron"
				{
					"\0\s[104]Heh,\w4 suddenly I feel like baking.\e"
				}
				elseif switchingtoshell == "Autumn Flannel"
				{
					if season == "Autumn"
					{
						"\0\s[104]Heh,\w4 anBlue's the one who introduced me to plaid.\w8\w8\s[205] I'd seen it before,\w4 of course,\w4 but I didn't realize how much I liked it until I tried it on.\e"
					}
					elseif season == "Winter"
					{
						"\0\s[106] It's a little late to be wearing this,\w4 isn't it?\e"
					}
					else
					{
						"\0\s[106]It's a little warm out for this,\w4 isn't it?\e"
					}
				}
				else //For any shell without a specific name, or if you don't want to bother with individual responses to a shell.
				{
					"\0\s[104]Done.\e"
				}
			}
			else
			{
				if LVCheck == 1
				{
					nowshell = tempshell
					"\0\s[510]* (Done.)\w8\e"
				}
				else
				{
					if tempshell == nowshell //[Zi] if this shell is the same as the last...
					{
						"\0\s[402]Somehow I think you're just trying to get under my skin.\e"
					}
					else
					{
						nowshell = tempshell //[Zi] and if it's not the same as the last, then change nowshell to reflect the current shell
						"\0\s[402]Happy now?\w8\e"
					}
				}
			}
		}
		booting = 0
}

OnDressupChanged
{
	dressups = ""
	if reference1 == "Green Beanie" && reference2 == 1
	{
		dressups += "Green Beanie, "
	}
}

//These handle switching between modes
OnSwitchS2RYS
{
	lastSclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
	_switchdialogue = "%(OnClose)"
	_switchdialogue = REPLACE(_switchdialogue,"\-","")
	_switchdialogue = REPLACE(_switchdialogue,"\e","")
	"%(_switchdialogue)\w4\0\s[-1]\b[-1]\1\s[-1]\_w[600]\![raise,OnDialogueVariables,switchRYS]\0\![raise,OnBoot]"
}

OnSwitchRYS2S
{
	lastRYSclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
	_switchdialogue = "%(OnClose)"
	_switchdialogue = REPLACE(_switchdialogue,"\RYS","\0")
	_switchdialogue = REPLACE(_switchdialogue,"\-","")
	_switchdialogue = REPLACE(_switchdialogue,"\e","")
	"%(_switchdialogue)\w4\0\s[-1]\b[-1]\_w[600]\![raise,OnDialogueVariables,switchS]\0\![raise,OnBoot]"
}

//For my own reference, when I had RYS on the third char, I had it use this command: \![get,property,OnGetModeSCoords,currentghost.scope(2).rect] Then go to these functions. Don't think I'll need them, but
/*OnGetModeSCoords
{
	shellchangexcoord = reference0[0]
	shellchangeycoord = reference0[1]
	"\0\![move,%(shellchangexcoord),%(shellchangeycoord)]\![raise,OnBoot]"
}

OnGetModeOtherCoords
{
	shellchangexcoord = reference0[0]
	shellchangeycoord = reference0[1]
	"\RYS\![move,%(shellchangexcoord),%(shellchangeycoord)]\![raise,OnBoot]"
}*/

//Variables to set on normal boots
BootVars
{
	lastTalk = ""
	passhour = 0
	passmin = 0
	passsec = 0
	givenfood = 0
	giveningredients = 0
	givenwarmthings = 0
	givenbooks = 0
	givencutethings = 0
	givenwater = 0
	flattered = 0
	complimented = 0
	comforted = 0
	lookingatphone = 0
	talkedaboutpast = 0
	lastinteracted = 0
	hugged = 0
	PollenAlert = 0
	todaysingredients = ""
	CameBack = 0
	askedalready = 1
	currentlydrinking = 0
	RYSrecentBlankets = 0
	RYSrecentBooks = 0
	Dragged = 0
	if changedscaling > 0; {changedscaling = 1}
	tempurature = "Unknown"
	ForC = "Unknown"
	feelslike = "Unknown"
	humidity = "Unknown"
	wind = "Unknown"
	weather = "Unknown"
	pollen = "Unknown"
	phase = "Unknown"
	booting = 0
	AutoUpdateCheck = 0
	holdingObsidian = 0
	if nowmode == "Asleep"; nowmode = "Default"
	holdingObsidian = 0
	sleephour = 0
	sleepmin = 0
	sleepsec = 0
	dreams = RAND(4)
	recentnightmare = 0
	
	if nowmode == "RYS"
	{
		RYSboots++
	}
	else
	{
		Sboots++
	}
	weatherquiet = 2 //setting this so we can check later and if it's 2, do a weather check earlier than an hour...
}

//Variables set after updates, because I have no self control
OnSetUpdateVariables
{
	if Update1p3p1 == ""
	{
		Update1p3p1 = "1.3.1"
		if "/l/" _in_ locationcode; locationcode = SPLIT(locationcode,"/l/")[1]
	}
	if Update1p3p0 == ""
	{
		Update1p3p0 = "1.3.0"
		holdingObsidian = 0
		heldObsidian = 0
		sleephour = 0
		sleepmin = 0
		sleepsec = 0
		slept = 0
		totalsleep = 0
		dreams = RAND(4)
		recentnightmare = 0
		//if "https://weather.com/weather/today/l/" !_in_ locationcode; locationcode = "https://weather.com/weather/today/l/" + locationcode
		
	}
	if Update1p2p1 == ""
	{
		Update1p2p1 = "1.2.1"
		dragdizzy = 0 //This is your fault, GrayArea
		scaledizzy = 0
	}
	if Update1p2p0 == "" //Update 1.2.0 variables - Same as below. I stg I'll add a version number in this update...
	{
		Update1p2p0 = "1.2.0"
		totalboops = 0
		weatherquiet = 0
		todaysingredients = ""
		totalwaters = 0
		currentlydrinking = 0
		switchingtoshell = ""
		CurrentXMonitor = 0
		CurrentYMonitor = 0
		CurrentXPos = 0
		CurrentYPos = 0
		NewXMonitor = 0
		NewYMonitor = 0
		XCoord = 0
		YCoord = 0
		//SinceTalk = 0
		FromDragging = 0
		CameBack = 0
		Dragged = 0
		beenbackwards = 0
		askedalready = 0
		talkedtoAzura = 0
		nowmode = "Default"
		RYS = "RYS-0825"
		RYStrust = 0
		RYSprogress = 0
		askedWhoRYS = 0
		knowaboutABI = 0
		askedaboutABI = 0
		RYSwater = 0
		RYSsnacks = 0
		RYSblankets = 0
		RYSbooks = 0
		RYSrecentBlankets = 0
		RYSrecentBooks = 0
		RYShugs = 0
		RYScomforted = 0
		RYShours = 0
		RYSpets = 0
		DeviceList = ""
		DeviceMemories = ""
		VLCSong = ""
		RYSboots = 0
		Sboots = 0
		RYSlearn = (0,0,0,0)
		talkedtoTemmie = 0
		MultiMonitor = 0
		givensocks = 0
		givenblankets = 0
		givenmittens = 0
		scaled = 0
		betaTester = "OFF"
		minimized = 0
		beenoffscreen = 0
		talkedtoLulo = 0
		installdate = ("","","","","","")
	}
	if poppable == "" //Update 1.1 variables - These wont be set if they've already been set once
	{
		DeviceMemories = ""
		DeviceList = '-' //start with -
		knowaboutkit = 0
		knowaboutsmol = 0
		continuousstroke = 0
		totalsnags = 0
		totalpunches = 0
		pethighscore = 0
		scroll = 0
		talkedtoDusty = 0
		AzuraSongMemories = ""
		staysafe = 0
		mailfrommenu = 0
		knowaboutbirdy = 0
		knowaboutsorrel = 0
		knowaboutpmd = 0
		knowaboutbender = 0
		knowaboutnine = 0
		knowaboutserif = 0
		knowaboutecho = 0
		knowaboutice = 0
		knowaboutomega = 0
		balloonspopped = 0
		poppable = "OFF"
		specialcommu = 0
		complimentaboutdusty = 0
		dustyspaceandtime = 0
		totalhugs = 0
		hugged = 0
		abbiblind = 0
		nowscale = 100
		changedscaling = 0
	}
	if SongMemories == "-"
	{
		SongMemories = ""
	}
}

//Variables for firstboot, and resetting
FirstVars
{
	lastTalk = "" //This is part of how chained conversations work (see aitalk.dic).
	passhour = 0
	passmin = 0 
	passsec = 0 // These two keep track of the time.
 	username         = "kid" //This sets a temporary name for the user. It'll be replaced when they tell the ghost their name.
	stroke           = 0	// Sets their being-pet state to zero.
 	teachusername    = 0	// Related to them learning your name.
	mikireflag       = 0	// Determines behavior at certain points.
 	aitalkinterval = 400 //Their default talk rate.
 	birthdayprint = "????" //A temporary value for the user's birthday.
	talktime = "5 minutes" //How the value aitalkinterval will display in the config menu.
	deleteshitai = "OFF" //Sets whether or not the user can uninstall the ghost to "OFF". The user can change it later in the config menu (see menu.dic). 
	nowshell = "red_hoodie" //This is for a special if check in aitalk.dic to see what shell the ghost is using. You should use the name of your default shell folder here.
	himher = "them"
	heshe = "they"
	hisher = "their"
	LV = 0
	love = 0
	SeenLV = 0
	LVCheck = 0
	broken = 0
	knowaboutfamily = 0
	thesplit = 0
	askedaboutwd = 0
	askedaboutdings = 0
	askedaboutwings = 0
	askedaboutbarcode = 0
	askedaboutbluejeans = 0
	askedaboutabbi = 0
	askedaboutrosie = 0
	askedaboutroman = 0
	askedaboutcambria = 0
	askedabouttriton = 0
	askedaboutlulo = 0
	askedaboutazo = 0
	askedaboutsigil = 0
	askedaboutelko = 0
	givenfood = 0
	giveningredients = 0
	givenwater = 0
	givenbooks = 0
	givenwarmthings = 0
	givencutethings = 0
	complimented = 0
	comforted = 0
	snagged = 0
	flattered = 0
	talkedaboutpast = 0
	knowmorethanyoushould = 0
	thatsanoddnumber = 0
	totalhours = 0
	lookingatphone = 0
	lastinteracted = 0
	totalpets = 0
	SeePC = "OFF"
	SeeNowPlaying = "OFF"
	SeeDiscord = "OFF"
	SongMemories = ""
	SongList = "-"
	//FoobarOpen = 0
	//FoobarMin = 0
	//WinampOpen = 0
	//WinampMin = 0
	//ItunesOpen = 0
	//ItunesMin = 0
	//SpotifyOpen = 0
	//SpotifyMin = 0
	//ActuallySpotify = 0
	//VLCOpen = 0
	//VLCMin = 0
	//DiscordOpen = 0
	//DiscordMin = 0
	//DiscordorChrome = 0
	//DevPalleteOpen = 0
	//WhichDev = 0
	//EVNovaOpen = 0
	//EVNovaMin = 0
	askedabouttimelines = 0
	AlreadyResponded = 0
	SpokeFirst = 0
	TempSong = ""
	AzuraSongList = "-"
	speech = ""
	recentpets = 0
	autoupdate = "ON"
	//-----update 1.1-----
	DeviceMemories = ""
	DeviceList = '-' //start with -
	knowaboutkit = 0
	knowaboutsmol = 0
	continuousstroke = 0
	totalsnags = 0
	totalpunches = 0
	pethighscore = 0
	scroll = 0
	talkedtoDusty = 0
	nowballoon = "Purple Soul"
	AzuraSongMemories = ""
	staysafe = 0
	mailfrommenu = 0
	knowaboutbirdy = 0
	knowaboutsorrel = 0
	knowaboutpmd = 0
	knowaboutbender = 0
	knowaboutnine = 0
	knowaboutserif = 0
	knowaboutecho = 0
	knowaboutice = 0
	knowaboutomega = 0
	balloonspopped = 0
	poppable = "OFF"
	specialcommu = 0
	complimentaboutdusty = 0
	dustyspaceandtime = 0
	totalhugs = 0
	hugged = 0
	abbiblind = 0
	nowscale = 100
	changedscaling = 0
	//---Update 1.2.0---
	installdate = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
	Update1p2p0 = "1.2.0"	
	totalboops = 0
	weatherquiet = 0
	todaysingredients = ""
	totalwaters = 0
	currentlydrinking = 0
	switchingtoshell = ""
	CurrentXMonitor = 0
	CurrentYMonitor = 0
	CurrentXPos = 0
	CurrentYPos = 0
	NewXMonitor = 0
	NewYMonitor = 0
	XCoord = 0
	YCoord = 0
	//SinceTalk = 0
	FromDragging = 0
	CameBack = 0
	Dragged = 0
	beenbackwards = 0
	askedalready = 0
	talkedtoAzura = 0
	nowmode = "Default"
	RYS = "RYS-0825"
	RYStrust = 0
	RYSprogress = 0
	askedWhoRYS = 0
	knowaboutABI = 0
	askedaboutABI = 0
	RYSwater = 0
	RYSsnacks = 0
	RYSblankets = 0
	RYSbooks = 0
	RYSrecentBlankets = 0
	RYSrecentBooks = 0
	RYShugs = 0
	RYScomforted = 0
	RYShours = 0
	RYSpets = 0
	DeviceList = ""
	DeviceMemories = ""
	VLCSong = ""
	RYSboots = 0
	Sboots = 0
	RYSlearn = (0,0,0,0)
	talkedtoTemmie = 0
	MultiMonitor = 0
	givensocks = 0
	givenblankets = 0
	givenmittens = 0
	scaled = 0
	betaTester = "OFF"
	minimized = 0
	beenoffscreen = 0
	talkedtoLulo = 0
	//---1.2.1---
	dragdizzy = 0
	scaledizzy = 0
	//---1.3.0---
	Update1p3p0 = "1.3.0"
	holdingObsidian = 0
	heldObsidian = 0
	sleephour = 0
	sleepmin = 0
	sleepsec = 0
	slept = 0
	totalsleep = 0
	dreams = RAND(4)
	recentnightmare = 0
	//---1.3.1---
	Update1p3p1 = "1.3.1"
	Degrees = "Fahrenheit"
	
	updateavailable = 0
	booting = 1 //This is here so that when you tell him your name the first time, he'll say the appropriate line.
}