//---------------------------Booting and Ending--------------------------

//--OnFirstBoot--


OnFirstBoot
{
	lastTalk = "" //This is part of how chained conversations work (see aitalk.dic).
	passhour = 0
	passmin = 0 
	passsec = 0 // These two keep track of the time.
	username         = "User" //This sets a temporary name for the user. It'll be replaced when they tell the ghost their name.
	stroke           = 0	// Sets their being-pet state to zero.
	mikireflag       = 0	// Determines behavior at certain points.
	aitalkinterval = 300 //Their default talk rate.
	birthdayprint = "????" //A temporary value for the user's birthday.
	talktime = "every 5 minutes" //How the value aitalkinterval will display in the config menu.
	deleteshitai = "OFF" //Sets whether or not the user can uninstall the ghost to "OFF". The user can change it later in the config menu (see menu.dic). 
	nowshell = "blue_sweater" //This is for a special if check in aitalk.dic to see what shell the ghost is using. You should use the name of your default shell folder here.
	booting = 1
	
	DefineStartVars //A function at the bottom that sets a whole mess of variables...
	--
	if reference0 == 0
 	{
		resets = 0
		"\![enter,passivemode]\0\s[990]\1\s[1610]\w7\s[10]\0\s[502]%(wdn)HH-!\w8\w8\s[208] AH,\w4 THIS IS...\w8\w8\1%(catnoises).\w8\w8\0\s[110]\n\n%(rgc)Oh-\w4\s[114] hello there.\w8\w8\s[0] My name is Dusty,\w4\s[104] it's good to meet you.\w8\w8\n\n\s[114]And you are..?\![open,inputbox,OnNameTeach,-1]"
 	}
	else
 	{
		resets++
		"\![enter,passivemode]\0\s[990]\1\s[1610]\w7\s[10]\0\s[502]%(wdn)HH-!\w8\w8\s[208] AH,\w4 THIS IS...\w8\w8\s[206] THIS IS...\w8\w8\1%(catnoises).\w8\w8\0\s[110]\n\n%(rgc)Oh-\w4\s[114] hello there.\w8\w8\s[0] My name is Dusty,\w4\s[104] it's good to meet you.\w8\w8\n\n\s[114]And you are..?\![open,inputbox,OnNameTeach,-1]"
 	}
}

//--GetTimeSlot

timeslot
{
	if hour >= 5 && hour <= 8
	{
		"earlymorning"
	}
	elseif hour >= 9 && hour <= 11
	{
		"morning"
	}
	elseif hour >= 12 && hour <= 14
	{
		"lunch"
	}
	elseif hour >= 15 && hour <= 17
	{
		"afternoon"
	}
	elseif hour >= 18 && hour <= 20
	{
		"evening"
	}
	elseif hour >= 21 && hour <= 24
	{
		"latenight"
	}
	else
	{
		"midnight"
	}
}


//--GetDaySlot

GetDaySlot
{
	"%(month)month %(day)day"
}


//-----------------------Normal Booting----------------------------

OnBoot
{
	OnBootVars
	_timesince = FUNCTIONEX("time_check.dll","how_many_days",lastclosed[0],lastclosed[1],lastclosed[2],0,0,0)//lastclosed[3],lastclosed[4],lastclosed[5] if you want the hours/minutes/seconds
	_timesince *= -1 //Multiply it by -1 to get rid of the negative sign, or to add one if the user traveled back in time.
	//The lastclosed array is set up in OnClose. Basically when you close him, it grabs the current time and saves it so that he can check it when he opens again.
	--
 	"\0\s[990]\1\s[1610]\w7\0%(wdc)" //Plays their animations before anything
	--
	dayslot = GetDaySlot
	
	if dayslot == userbirthday
	{
		"\0\s[0]%(t)Hello##]\w4 %(username).\w8\w8\s[114] %(t)It's your birthday##\w4 isn't it?\w8\w8\s[104\] Happy birthday.]\w8\w8\1%(holdnoise)\e"
	}
	elseif WDOnly == 1
	{
		WingdingsBoot
	}
	else
	{
		if _timesince >= 730 //2 years
		{
			ObsiLove = 0
			"\0\s[110]Ah-!\w8\s[114] Hello again,\w4 %(username).\w8\w8\s[104] It's been a very long time,\w4 I'm glad to see you.\w8\w8\s[0] How have you been?\w8\w8 Hopefully well?\w8\w8\1\s[1002]%(annoyednoises)\e"
		}
		elseif _timesince >= 365 //A year
		{
			ObsiLove -= 80; if ObsiLove < 0; {ObsiLove = 0}
			"\0\s[110]Ah-!\w8\s[114] Hello %(username),\w4 it is good to see you.\w8\w8\s[0] It's been more than a year in your timeline,\w4 hasn't it?\w8\w8\s[114] How have things been?\w8\w8\1%(annoyednoises)\e"
		}
		elseif _timesince >= 180 //Half a year
		{
			ObsiLove -= 40; if ObsiLove < 0; {ObsiLove = 0}
			"\0\s[114]It has been a while,\w4 %(username).\w8\w8\s[208] More than half a year in your timeline,\w4 I believe?\w8\w8\s[0] How have things been there?\w8\w8 Are you doing well?\w8\w8\1\s[1003]%(annoyednoises)\e"
		}
		elseif _timesince >= 60 //2 months
		{
			ObsiLove -= 20; if ObsiLove < 0; {ObsiLove = 0}
			"\0\s[114]Ah,\w4 hello again %(username).\w8\w8\1\s[10]%(catnoises).\w8\w8\0\s[0]\n\nIt has been a few months since I was here last,\w4 have you been well?\e"
		}
		elseif _timesince >= 30 //A month
		{
			"\0\s[0]Hello again,\w4 %(username).\w8\w8\s[114] It's been a while since I saw you last,\w4 how have you been?\1%(catnoises).\e"
		}
		elseif _timesince >= 14 //Two weeks
		{
			"\0\s[0]Hello again,\w4 %(username).\w8\w8\s[114] It's been a few weeks since I saw you last,\w4 have you been well?\w8\w8\1%(catnoises).\e"
			"\0\s[0]Hello again,\w4 %(username).\w8\w8\s[114] How have you been these past few weeks?\w8\w8\1%(catnoises).\e"
			if day < 14
			{
				"\0\s[0]Hello again,\w4 %(username).\w8\w8\s[114] It's been a little while,\w4 How is your %(displaymonth) going?\w8\w8\1%(catnoises).\e"
			}
		}
		elseif _timesince < 0 //Negative value, the user has set their clock back
		{
			"\0\s[110]Ah...\w8\w8\s[208] It's been...\w8\w8 %(_timesince) days since we were here?\w8\w8\s[106] %(username),\w4 did you invent time travel,\w4 or have you been changing your computer's clock?\w8\w8\1%(catnoises).\e"
		}
		elseif "RYS-0825" _in_ ghostexlist && talkedtoS == 1
		{
			res_reference0 = "RYS-0825"
			"\e DustyGetStatsBoot"
		}
		else
		{
			normalboottalk
		}
	}
}

WingdingsBoot
{
	if teachFont == "Wingdings"
	{
		"\0\s[104]Hello again,\w4 %(username).\w8\w8\1%(catnoises).\w8\w8\0\s[0]\n\nLast time we were here,\w4 you asked me to speak only in Wingdings.\w8\w8 Would you like me to continue speaking only in Wingdings?\n\n/
		\![*]\q[Keep speaking in Wingdings,bootwd]\n/
		\![*]\q[Speak normally\, please,bootnonwd]\e"
	}
	else
	{
		"\0\s[104]Hello again,\w4 %(username).\w8\w8\1%(catnoises).\w8\w8\0\s[0]\n\nLast time we were here,\w4 you asked me to speak mostly in %(teachFont).\w8\w8 Would you like me to continue speaking in %(teachFont)?\n\n/
		\![*]\q[Keep speaking in %(teachFont),bootwd]\n/
		\![*]\q[Speak normally\, please,bootnonwd]\e"
	}
}

Select.bootwd
{
	"%(wdc)\0\s[104]%(t)Ok##\w4 I will continue to speak in] %(teachFont).\w8\w8\1%(catnoises).\e"
}

Select.bootnonwd
{
	WDOnly = 0
	"\0\s[104]Ok,\w4 if you change your mind you can toggle the option in the config menu.\w8\w8\1%(catnoises).\e"
}

//--normalboottalk

//This is another unique bit of coding from cindysuke's ghost. OnBoot above checks for the day when you open them, but normalboottalk will vary their dialogue in a few different ways depending on the time and weekday. If you don't want to use these, you can delete everything under normalboottalk and write your own lines of general boot dialogue, so it'd look something like.


normalboottalk //%(wdc) isn't working here, I assume something to do with it booting up... I think I'd rather him ask the user if they want to stay in wingdings only mode anyways.
{
	if RAND(100) < 40
	{
		"\0\s[104]Hello again,\w4 %(username).\w8\w8\1%(catnoises).\e"
	}
 	else
 	{
		if timeslot == "earlymorning" //5am-8am
		{
			"\0\s[104]Hello %(username).\w8\w8\1%(catnoises).\w8\w8\0\s[0]\n\nRising early today?\e"
		}
		elseif timeslot == "morning" //9am-11am
		{
			"\0\s[0]Good morning,\w4 %(username).\w8\w8\1%(catnoises).\e"
		}
		elseif timeslot == "lunch" //12pm-2pm
		{
			"\0\s[0]Hello %(username).\w8\w8 Enjoying the sunlight today?\w8\w8\1%(catnoises).\w8\w8\0\s[106]\n\nOr perhaps it's cloudy where you are.\e"
		}
		elseif timeslot == "afternoon" //3pm-5pm
		{
			"\0\s[0]Good afternoon,\w4 %(username).\w8\w8\1%(catnoises).\e"
		}
		elseif timeslot == "evening" //6pm-8pm
		{
			"\0\s[0]Good evening,\w4 %(username).\w8\w8\1%(catnoises).\e"
		}
		elseif timeslot == "latenight" //9pm-12am
		{
			"\0\s[0]Good evening,\w4 %(username).\w8\w8\1%(catnoises).\0\w8\w8\n\nHaving a late night?\e"
		}
		else //Midnight, 1am-4am
		{
			"\0\s[0]Hello,\w4 %(username).\w8\w8\1%(catnoises).\w8\w8\0\n\n\s[106]It's late where you are,\w4 isn't it?\e"
		}
	}
}


//--OnClose

OnClose
{
	lastclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
	"%(wdc)"
	--
	if holdingObsidian == 1
	{
		if passmin >= 5 || passhour > 0
		{
			if RAND(100) < 40
			{
				"\0\s[600]Farewell,\w4 %(username)."
				"\0\s[600]Take care,\w4 %(username)."
				"\0\s[600]Until we meet again."
			}
			else
			{
				if timeslot == "earlymorning" //5am-8am
				{
					if passhour > 3
					{
						"\0\s[606]Ah,\w4 just off to bed?\w8\w8\s[102] Sleep well,\w4 %(username)."
					}
					else
					{
						"\0\s[600]Have a good day,\w4 %(username)."
					}
				}
				elseif timeslot == "morning" //9am-11am
				{
					"\0\s[600]Take care,\w4 %(username).\w8\w8 There's a lot of sunlight left today,\w4 don't waste it."
				}
				elseif timeslot == "lunch" //12pm-2pm
				{
					"\0\s[600]Off for lunch?\w8\w8\s[104] Alright,\w4 I'll see you later."
				}
				elseif timeslot == "afternoon" //3pm-5pm
				{
					"\0\s[600]Off to enjoy the last of the sunlight?\w8\w8\s[102] Take care."
				}
				elseif timeslot == "evening" //6pm-8pm
				{
					"\0\s[600]Off for the night?\w8\w8\s[104] Enjoy your evening,\w4 %(username)."
				}
				elseif timeslot == "latenight" //9pm-12am
				{
					"\0\s[600]Off to bed?\w8\w8\s[104] Sleep well,\w4 %(username)."
				}
				else //Midnight, 1am-4am
				{
					"\0\s[600]Good night,\w4 %(username).\w8\w8\s[0] Sleep well."
				}
			}		
		}	
		else //This dialogue is for if they haven't been open for at least five minutes.
		{
			"\0\s[600]Ah,\w4 that was quick.\w8\w8 Did you call us by mistake?\w8\w8\s[104] Take care,\w4 %(username)."
		}
		--
		"\w8\w8\1%(holdnoise)\w8\w8\-\e" //this bit of code actually closes and ends the ghost. Because \e is here, you don't have to end each line above with it. Again, this will come up very rarely, so OnClose is an unusual case.
	}
	else
	{
		if passmin >= 5 || passhour > 0
		{
			if RAND(100) < 40
			{
				"\0\s[102]Farewell,\w4 %(username)."
				"\0\s[104]Take care,\w4 %(username)."
				"\0\s[102]Until we meet again."
			}
			else
			{
				if timeslot == "earlymorning" //5am-8am
				{
					if passhour > 3
					{
						"\0\s[106]Ah,\w4 just off to bed?\w8\w8\s[102] Sleep well,\w4 %(username)."
					}
					else
					{
						"\0\s[104]Have a good day,\w4 %(username)."
					}
				}
				elseif timeslot == "morning" //9am-11am
				{
					"\0\s[102]Take care,\w4 %(username).\w8\w8 There's a lot of sunlight left today,\w4 don't waste it."
				}
				elseif timeslot == "lunch" //12pm-2pm
				{
					"\0\s[0]Off for lunch?\w8\w8\s[104] Alright,\w4 I'll see you later."
				}
				elseif timeslot == "afternoon" //3pm-5pm
				{
					"\0\s[0]Off to enjoy the last of the sunlight?\w8\w8\s[102] Take care."
				}
				elseif timeslot == "evening" //6pm-8pm
				{
					"\0\s[0]Off for the night?\w8\w8\s[104] Enjoy your evening,\w4 %(username)."
				}
				elseif timeslot == "latenight" //9pm-12am
				{
					"\0\s[0]Off to bed?\w8\w8\s[104] Sleep well,\w4 %(username)."
				}
				else //Midnight, 1am-4am
				{
					"\0\s[102]Good night,\w4 %(username).\w8\w8\s[0] Sleep well."
				}
			}		
		}	
		else //This dialogue is for if they haven't been open for at least five minutes.
		{
			"\0\s[0]Ah,\w4 that was quick.\w8\w8 Did you call us by mistake?\w8\w8\s[104] Take care,\w4 %(username)."
		}
		--
		"\w8\w8\1%(sCatNoise)\w8\w8\-\e"
	}
 }


 //-------------------------State Changing--------------------
 //From here on, most of the dialogue will have to do with changing shells, ghosts, or states in some way.
 

//--OnWindowStateRestore

//This will run when you minimize them and then bring them back up. Replace with what you like.

OnWindowStateRestore
{
	if sHoldingObsidian == 1 || SomeoneHoldingObsidian != ""
	{
		"\0%(wdc)\s[110]%(t)Ah...\w8\w8\0\s[208\] That was...\w8\w8 strange.\w8\w8\s[206\]\n\nNot entirely unlike the void...]\w8\w8\1%(sCatNoise)\e"
	}
	else
	{
		"\1\s[10]\0%(wdc)\s[110]%(t)Ah...\w8\w8\0\s[208\] That was...\w8\w8 strange.\w8\w8\s[206\]\n\nNot entirely unlike the void...]\w8\w8\1%(sCatNoise)\e"
	}
}

OnWindowStateMinimize
{
	if SomeoneHoldingObsidian != ""
	{
		res_reference0 = SomeoneHoldingObsidian[0]
		_ghost = SomeoneHoldingObsidian[0]
		_num = SomeoneHoldingObsidian[1]
		SomeoneHoldingObsidian = ""
		
		"\e DustyMinimized%(_ghost) %(_num)"
	}
}


//--OnGhostChanging
 
OnGhostChanging
 {
	"%(wdc)"
	--
	if holdingObsidian == 1
	{
		if reference0 == "Dusty" //Replace this with the name of your Sakura character as you put in install.txt or descript.txt.
		{
			"\0\s[600]%(t)Alright##\w4 just a moment.]\w8\w8\e"
		}
		else //If you're switching to another Ghost.
		{
			lastclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
			
			if reference0 == "RYS-0825"
			{
				"\0\s[600]%(t)Alright##\w4 I'll fetch S for you.]\w8\w8\e"
			}
			else
			{
				"\0\s[600]%(t)Alright##\w4 I'll let] %(reference0) %(t)know you want them.]\w8\w8\e"
			}
			//In OnGhostChanging, %(reference0) stores the name of the ghost being swapped to.
		}
	}
	else
	{
		if reference0 == "Dusty" //Replace this with the name of your Sakura character as you put in install.txt or descript.txt.
		{
			"\0\s[0]%(t)Alright##\w4 just a moment.]\w8\w8\e"
		}
		else //If you're switching to another Ghost.
		{
			lastclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)")
			
			if reference0 == "RYS-0825"
			{
				"\0\s[0]%(t)Alright##\w4 I'll fetch S for you.]\w8\w8\e"
			}
			else
			{
				"\0\s[0]%(t)Alright##\w4 I'll let] %(reference0) %(t)know you want them.]\w8\w8\e"
			}
			//In OnGhostChanging, %(reference0) stores the name of the ghost being swapped to.
		}

	}
}


//--OnGhostChanged

//A sister to the above, this runs after the ghost has either reloaded itself, or when you switch from another ghost to this ghost.

OnGhostChanged
{
	if sHoldingObsidian == 0 && SomeoneHoldingObsidian == ""
	{
		"\1\s[10]"
	}
	--
	"\0\s[0]%(wdc)" //this sets up their default poses
	--
	if holdingObsidian == 1
	{
		if reference0 == "Dusty" //As above, this should be the name of your ghost as in install.txt or descript.txt.
		{
			obsiJumping = 0
			
			"\1\s[-1]\0\s[600]%(t)I'm back.\w8\w8 Is everything alright?]\e"
		}
		else //If you're switching to them from another ghost...
		{
			OnBootVars
			--
			holdingObsidian = 0
			if WDOnly == 1
			{
				"\0\s[104]Hello again,\w4 %(username).\w8\w8\1%(catnoises).\w8\w8\0\s[0]\n\nLast time we were here,\w4 you asked me to speak only in Wingdings.\w8\w8 Would you like me to continue speaking only in Wingdings?\n\n/
				\![*]\q[Keep speaking in Wingdings,bootwd]\n/
				\![*]\q[Speak normally\, please,bootnonwd]\e"
			}
			else
			{
				"Hello,\w4 %(username).\w8\w8 Giving %(reference0) a break?\e"
			}
			//Again, %(reference0) in this function is the name of the ghost in question. As above, replace these two lines of dialogue what what'd be appropriate for your ghost.
		}
	}
	else
	{
		if reference0 == "Dusty" //As above, this should be the name of your ghost as in install.txt or descript.txt.
		{
			"%(t)I'm back.\w8\w8 Is everything alright?]\e"
		}
		else //If you're switching to them from another ghost...
		{
			OnBootVars
			--
			if WDOnly == 1
			{
				WingdingsBoot
			}
			else
			{
				"Hello,\w4 %(username).\w8\w8 Giving %(reference0) a break?\e"
			}
			//Again, %(reference0) in this function is the name of the ghost in question. As above, replace these two lines of dialogue what what'd be appropriate for your ghost.
		}
	}
}


//--OnShellChanging

//This will run when you change your ghost's shell.

OnShellChanging
{
	tempshell = TOLOWER(reference0) //changes it to lowercase
	tempshell = REPLACE(tempshell," ","_") //replace spaces with underscores. This should work as long as my naming schemes never change |D
	"%(wdc)"
	changing = 1
	--
	if holdingObsidian == 1
	{
		if tempshell == nowshell //If you're switching to the same shell
		{
			"\0\s[606]%(t)Ah...\w8\w8 I'm already wearing that##\w4 but alright.]\w8\w8\e"
		}
		elseif reference0 == "Blue Sweater" //reference0 in OnShellChanging is the name of the shell you're swapping to.
		{
			"\0\s[600]%(t)My sweater should be around here somewhere...]\w8\w8\e"
		}
		else //if you don't have any specific dialogue for a specific shell
		{
			"\0\s[600]%(t)Ok##\w4 just a moment.]\w8\w8\e"
		}
	}
	else
	{
		if tempshell == nowshell //If you're switching to the same shell
		{
			"\0\s[106]%(t)Ah...\w8\w8 I'm already wearing that##\w4 but alright.]\w8\w8\e"
		}
		elseif reference0 == "Blue Sweater" //reference0 in OnShellChanging is the name of the shell you're swapping to.
		{
			"\0\s[208]%(t)My sweater should be around here somewhere...]\w8\w8\e"
		}
		else //if you don't have any specific dialogue for a specific shell
		{
			"\0\s[0]%(t)Ok##\w4 just a moment.]\w8\w8\e"
		}
	}
}
 
 
 //--OnShellChanged
 
 //The sister to the above, this is the dialogue after you've changed the shell.
 
OnShellChanged
{
	if booting != 2
	{
		tempshell = TOLOWER(reference0) //changes it to lowercase
		tempshell = REPLACE(tempshell," ","_") //replace spaces with underscores. This should work as long as my naming schemes never change |D
		"%(wdc)"
		--
		if holdingObsidian == 1
		{
			if tempshell == nowshell
			{
				"\1\s[-1]\0\s[606]%(t)...Nothing has changed.]\w8\w8\1Mrrr.\e"
			}
			elseif reference0 == "Blue Sweater"
			{
				nowshell = "blue_sweater"
				
				"\1\s[-1]\0\s[600]%(t)I've had this sweater a long time.\w8\w8\s[606\] It was actually given to me by someone dear to me##\w4 a very long time ago...]\e"
			}
			else //For any shell without a specific name, or if you don't want to bother with individual responses to a shell.
			{	
				nowshell = TOLOWER(reference0) //This shouldnt come up, but I HOPE that if someone used a shell that he doesnt recognize, it'd still set the nowshell correctly so that he can recognize it up in OnShellChanging
				nowshell = REPLACE(nowshell," ","_")
				"\1\s[-1]\0\s[600]%(t)Now using] %(reference[0]).\w8\w8\1%(holdnoise).\e"
			}
		}
		else
		{
			if tempshell == nowshell
			{
				"\1\s[10]\0\s[106]%(t)...Nothing has changed.]\w8\w8\1\s[10]Mrrr.\e"
			}
			elseif reference0 == "Blue Sweater"
			{
				nowshell = "blue_sweater"
				
				"\1\s[10]\0\s[102]%(t)I've had this sweater a long time.\w8\w8\s[200\] It was actually given to me by someone dear to me##\w4 a very long time ago...]\e"
			}
			else //For any shell without a specific name, or if you don't want to bother with individual responses to a shell.
			{	
				nowshell = TOLOWER(reference0) //This shouldnt come up, but I HOPE that if someone used a shell that he doesnt recognize, it'd still set the nowshell correctly so that he can recognize it up in OnShellChanging
				nowshell = REPLACE(nowshell," ","_")
				"\1\s[10]\0\s[0]%(t)Now using] %(reference[0]).\w8\w8\1%(sCatNoise)\e"
			}
		}
	}
	else
	{
		"\![raise,OnUpdateComplete]"
	}
}


 //--OnDressupChanged
/*
OnDressupChanged
{
	"%(wdc)"
	--
if reference0 == 0 //in OnDressupChanged, reference0 refers to the character, therefore this is evaluating which character in question is getting dressed up. 0 is the main character, as you know from the walkthrough by now.
	{
	if reference1 == "Clothing for Girl" //This is the name of the clothing item in the descript.txt file, I'm pretty sure.
		{
			if reference2 == 1 //This checks if you're putting it on.
			{
			"\0\s[0]You put my clothing item on me.\w8\1\s[19]lol like zar would ever bother with clothes for a ghost.\w8\0\s[9]\n\n[half]Shut up dude you're going to get us in trouble.\e"
			}
			else // This checks if you're taking it off.
			{
			"\0\s[0]You took the clothing item off.\e"
			}
		}		
	}
else //if reference0 isn't the main character, then it has to be the side character.
	{
	if reference1 == "some kind of clothing for triangle??" //The name of the item of clothing in the descript.txt file in your shell folder.
		{
			if reference2 == 1 //If you put it on
			{
			"\1\s[10]I'm wearing some kind of clothing.\w8\0\s[8]Maybe a hat?\e"
			}
			else //When you take it off.
			{
			"\1\s[10]You took off some kind of clothing.\e"
			}
		}
	elseif reference1 == "i'm gonna say a propeller beanie" //As you've seen above, you can use elseifs to expand the list if you have more than one item.
		{
			if reference2 == 1 //if you're putting it on
			{
			"\1\s[10]You put a hat on me.\w8\0\s[209]lol.\w8\1\n\n[half]\s[11]Shaddap.\e"
			}
			else //when you take it off
			{
			"\1\s[10]You took off the hat.\e"
			}
		}
	}
}

//If you are super dedicated to doing clothes, I'd recommend checking out some more in-depth FLELE tutorials for stuff about clothing until I muster up the energy to write a walkthrough page for it.
*/

OnBootVars //These run every time he starts, whether you've just changed ghosts or called him or whatever
{
	passsec = 0
	passmin = 0
	passhour = 0
	recenttreats = 0
	tempurature = "Unknown"
	ForC = "Unknown"
	feelslike = "Unknown"
	humidity = "Unknown"
	wind = "Unknown"
	weather = "Unknown"
	pollen = "Unknown"
	phase = "Unknown"
	sunrise = "Unknown"
	sunset = "Unknown"
	holdingObsidian = 0
	lastTalk = ""
	booting = 0
	weatherquiet = 2 //So he does a weather check if you have it on
	sHoldingObsidian = 0
	SomeoneHoldingObsidian = ""
	obsiJumping = 0
	if talkedtoElsen == 1; talkedtoElsen = 2
	if talkedtoMiniDev == 1; talkedtoMiniDev = 2
}

UpdateVars
{
	if InstalledUpdates[5] != "1.0.6"
	{
		InstalledUpdates[5] = "1.0.6"
		SomeoneHoldingObsidian = ""
		obsiJumping = 0
		talkedtoElsen = 0
		talkedtoMiniDev = 0
		SomeoneHeldObsidian = (0,"")
	}
	if Update1p0p4 != "1.0.4"
	{
		Update1p0p4 = "1.0.4"
		if "/l/" _in_ locationcode; locationcode = SPLIT(locationcode,"/l/")[1]
		Degrees[0] = "Fahrenheit"
		Degrees[1] = "°F"
		_temp = tempurature
		tempurature[0] = _temp
		tempurature[1] = _temp
		tempurature[2] = _temp
		sHoldingObsidian = 0
	}
	if Update1p0p2 != "1.0.2"
	{
		Update1p0p2 = "1.0.2"
		if "https://weather.com/weather/today/l/" !_in_ locationcode; locationcode = "https://weather.com/weather/today/l/" + locationcode
	}
	if Update1p0p1 != "1.0.1"
	{
		Update1p0p1 = "1.0.1"
		installdate = "Unknown"
	}
}

DefineStartVars //Variables that need to be set on firstboot
{
	tooltips = "Off"
	holdingObsidian = 0
	recenttreats = 0
	autoupdate = "ON"
	askedalready = 1 //So he doesn't immediately tell you after firstboot
	updateavailable = 0
	totalminutes = 0
	totalhours = 0
	jumpMenu = 0
	currentlytranslating = ""
	dayslot = ""
	userbirthday = ""
	WDOnly = 0
	SpokeFirst = 0
	tempshell = ""
	checkname = ""
	talkedtoS = 0
	metOtherGhosts = 0
	notfirstresponse = 0
	AlreadyResponded = 0
	fromCheck = 0
	teachWD = "Off"
	teachPROPER = "Off"
	teachUPPER = "On"
	teachLOWER = "Off"
	teachPUNCT = "Off"
	teachNUM = "Off"
	curlocation = "Set a location"
	fromfunctionmenu = 0
	loadmoonnum = 0
	G = 0
	currentStreakTotal = 0
	answerprint = ""
	CTGdusty = "On"
	CTGukagaka = "Off"
	CTGskeleton = "Off"
	CTGundertale = "Off"
	CTGspacefi = "Off"
	CTGgems = "Off"
	CTGshapes = "Off"
	CTGingredients = "Off"
	CTGother = "Off"
	CTGuser = "Off"
	UserCategories = IARRAY
	AutoWeather = "OFF"
	askedaboutobsidian = 0
	askedabouthim = 0
	treatsgiven = 0
	askedaboutwingdings = 0
	hemisphere = "Northern"
	ObsiLove = 0
	catpets = 0
	catboops = 0
	beansSquished = 0
	totalmousetoys = 0
	totalbelltoy = 0
	totalpingpong = 0
	totalcatnip = 0
	Dcontinuousstroke = 0
	totalpets = 0
	Dcurrentpetstreak = 0
	Ocontinuousstroke = 0
	Ocurrentpetstreak = 0
	Dpethighscore = 0
	Opethighscore = 0
	Dtoppetstreak = 0
	Otoppetstreak = 0
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	spentG = 0
	pickedUp = 0
	totalhugs = 0
	confided = 0
	gushed = 0
	confidedObsi = 0
	opinionObsi = 0
	allergicPollen = "Unknown"
	possibleanswers = 3
	weatherquiet = 0
	tempurature = "Unknown"
	ForC = "Unknown"
	feelslike = "Unknown"
	humidity = "Unknown"
	wind = "Unknown"
	weather = "Unknown"
	pollen = "Unknown"
	phase = "Unknown"
	userfont = IARRAY
	userfontsize = IARRAY
	teachFont = "Wingdings"
	callingGhost = 0
	SeePC = "Off"
	installdate = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)","%(dustyver)") //dustyver added in 1.0.5
	//---1.0.4---
	Update1p0p4 = "1.0.4"
	Degrees[0] = "Fahrenheit"
	Degrees[1] = "°F"
	tempurature[0] = 0
	tempurature[1] = 0
	//---1.0.6---
	InstalledUpdates[5] = "1.0.6"
	SomeoneHoldingObsidian = ""
	obsiJumping = 0
	talkedtoElsen = 0
	talkedtoMiniDev = 0
	SomeoneHeldObsidian = (0,"")
}