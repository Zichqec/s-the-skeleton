//---------------------------Mouse Functions--------------------------


//This is a bunch of code determining who the mouse is over. Don't touch any of it.
OnMouseMove
{
	//---- reference3 determines the character in question
	if reference3 == 0 //if the character is 0, aka the main character
	{
		//---- it will run the mouse functions for that character
		MouseMoveSakura
	}
	else //if the character isn't 0, then it must be the side character.
	{
		//---- this will run the mouse functions for the side character.
		MouseMoveKero
	}
}


//--MouseMoveSakura
//The petting function for your main character. Remember how it was referred to as sakura in some of the descript.txt and install.txt files?
 
MouseMoveSakura
{
	if reference4 == "Head"
	{
		stroke++ //Stroke value only goes up when over the appropriate areas
		Dcontinuousstroke++
		
		if Dcontinuousstroke >= 160 //this would happen starting at 4 pets
		{
			if stroke >= 160 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
			{
				stroke = 0
				totalpets++
				Dcurrentpetstreak++
				"\0\s[107]* (His skull warms up as you stroke over and over.)"
				"\0\s[107]* (You can't help petting him over and over.)"
				"\0\s[107]* (If you pet him too much you're going to wear out what little skull he has left.)"
				if sHoldingObsidian != 1
				{
					--
					"\1%(catnoises)!\w8\w8\0\n\n* (Obsidian looks like she wants some too.)\e"
					"\1%(catnoises)!\w8\w8\0\n\n* (How much longer can you ignore her pleas?)\e"
					"\1%(catnoises)!\w8\w8\0\n\n* (The cat desires affection also.)\e"
					"\e"
					"\e"
				}
			}
		}
		elseif stroke >= 40
		{
			stroke = 0 //Stroke value is reset
			totalpets++
			Dcurrentpetstreak++
			totalpets++
			if totalpets < 20
			{
				if holdingObsidian == 1
				{
					"\0\s[606]"
					"\0\s[607]"
				}
				else
				{
					"\0\s[106]"
					"\0\s[107]"
				}
				--
				"%(wdc)%(t)...\w4\s[106\]Thank you##]\w4 %(username).\e"
				"%(wdc)%(t)...I'm not used to anyone being tall enough to pet me.]\e"
				"%(wdc)%(t)...Obsidian is probably softer to pet##\w4 you know.]\e"
			}
			else
			{
				if holdingObsidian == 1
				{
					"\0\s[600]"
				}
				else
				{
					"\0\s[102]"
				}
				--
				"%(wdc)* (He holds very still as you gently stroke his skull.)"
				"%(wdc)* (He doesn't complain as you stroke your cursor over his skull.)"
				"%(wdc)* (He stands quietly as you pet him.)"
				"%(wdc)* (He doesn't say anything,\w4 but you're pretty sure he's enjoying this.)"
				"%(wdc)* (He doesn't comment,\w4 but you're pretty sure he appreciates the attention.)"
				"%(wdc)* (His skull is very smooth and oddly satisfying to stroke.)\w8\w8\n\n* (At least,\w4 the parts of it that haven't crumbled.)"
				if holdingObsidian == 1
				{
					"\s[607]%(wdc)* (You stroke too close to the seams and your cursor starts feeling a little numb.)"
				}
				else
				{
					"\s[107]%(wdc)* (You stroke too close to the seams and your cursor starts feeling a little numb.)"
				}	
				--
				if holdingObsidian == 1
				{
					"\1%(holdnoise)\e"
					"\e"
				}
				elseif sHoldingObsidian == 1
				{
					"\e"
				}
				else
				{
					"\1%(catnoises).\w8\w8\0\n\n* (Looks like Obsidian is a little jealous.)\e"
					"\1%(catnoises).\w8\w8\0\n\n* (Maybe you should pet Obsidian a little,\w4 too.)\e"
					"\1%(catnoises).\w8\w8\0\n\n* (Obsidian looks like she could use some attention,\w4 too.)\e"
					"\1%(catnoises).\w8\w8\0\n\n* (How dare you neglect your queen.)\e"
					"\e"
					"\e"
					"\e"
					"\e"
					"\e"
					"\e"
					"\e"
					"\e"
					"\e"
				}
			}
		}
	}
	elseif reference4 == "Seam"
	{
		stroke++
		
		if stroke >= 40
		{
			stroke = 0
			if holdingObsidian == 1
			{
				"\0\s[606]"
			}
			else
			{
				"\0\s[107]"
				"\0\s[110]"
			}
			--
			"%(wdc)* (Your cursor slips into the void on his face.)\w8\w8\n\n\s[106]%(t)...My skull isn't exactly whole##\w4 anymore.]\e"
			if holdingObsidian == 1
			{
				"%(wdc)* (Your cursor slips into the void on his face,\w4 and comes away feeling a little numb.)\w8\w8\n\n\s[606]%(t)Ah## please be careful## %(username).\w8\w8\s[607\] My skull isn't as whole as it looks.]\e"
			}
			else
			{
				"%(wdc)* (Your cursor slips into the void on his face,\w4 and comes away feeling a little numb.)\w8\w8\n\n\s[106]%(t)Ah## please be careful## %(username).\w8\w8\s[107\] My skull isn't as whole as it looks.]\e"
			}
		}
	}
	elseif reference4 == "Obsidian" //For when he's holding her
	{
		stroke++
		if stroke >= 40
		{
			catpets++
			ObsiLove++
			stroke = 0
			"\1%(holdnoise)"
			--
			"\0\s[630]%(wdf)WHAT ARE YOU EVEN GOING TO DO WITH ALL THIS ATTENTION?\e"
			"\w8\w8\w8\n\n* (She seems very content right now.)\e"
			"\e"
			"\e"
			"\e"
			"\e"
			"\e"
		}
	}
}

 //---- MouseMoveKero -----------------------------------------------------------------
 //MouseMoveKero is exactly like the above, except set up for your second character. 
 
MouseMoveKero
{
	if reference4 == "Head"
	{
		stroke++ //Stroke value only goes up when over the appropriate areas
		Ocontinuousstroke++
		
		/*if Ocontinuousstroke >= 160 //this would happen starting at 4 pets
		{
			if stroke >= 160 //Stroke count is longer so that you have time to read the dialogue here. Adjust as you like.
			{
				stroke = 0
				catpets++
				Ocurrentpetstreak++
				ObsiLove += 3
				"\1\s[1004]* (She relaxes under your cursor,\w4 purrs emanating from deep within.)\e"
				"\1\s[1004]* (She purrs continuously as you pet continuously.)\e"
				"\1\s[1004]* (You can feel the soft rumble of her purring.)\e"
				"\1\s[1004]* (She seems content.)\e"
			}
		}
		else*/if stroke >= 40
		{
			stroke = 0 //Stroke value is reset
			catpets++
			Ocurrentpetstreak++
			ObsiLove++
			"\1\s[1003]* (You scritch behind her ears.)\w8\w8\n\n%(goodnoises)\e"
			"\1\s[1003]* (You pat her head softly.)\w8\w8\n\n%(goodnoises)\e"
			"\1\s[1003]* (You stroke the soft fur on her head.)\w8\w8\n\n%(goodnoises)\e"
			"\1\s[1003]* (You rub the soft fur under her chin.)\w8\w8\n\n%(goodnoises)\e"
		}
	}
	elseif reference4 == "Body" && SHIORI3FW.CanTalk == 1 //This is here so you dont accidentally pet while her menu is open, hopefully
	{
		stroke++
		if stroke >= 50
		{
			ObsiLove++
			stroke = 0
			"\1%(catpose)* (You stroke your cursor down her back.)\w8\w8\n\n%(catpose)* (She leans into it.)\w8\w8\n\n%(goodnoises)\e"
			"\1%(catpose)* (You scratch between her shoulders.)\w8\w8\n\n%(goodnoises)\e"
			"\1%(catpose)* (You rub down her sides.)\w8\w8\n\n%(goodnoises)\e"
			"\1%(catpose)* (You smooth out the fur on her back.)\w8\w8\n\n%(goodnoises)\e"
			"\1%(catpose)* (You scratch the sides of her neck.)\w8\w8\n\n%(goodnoises)\e"
		}
	}
}

OnMouseLeaveAll //When the mouse leaves the ghost. You can change to OnMouseLeave if you want it to happen when you leave that specific hitbox
{
	if Dcontinuousstroke > 160 && Dcontinuousstroke > Dpethighscore //Checks if you're in continuous petting, and if you are, if you got a high score. \C will display the previous balloon again.
	{
		Dpethighscore = Dcontinuousstroke
		"\C\n\n* (New high score! %(Dpethighscore)!)\e"
	}
	if Ocontinuousstroke > 160 && Ocontinuousstroke > Opethighscore //Checks if you're in continuous petting, and if you are, if you got a high score. \C will display the previous balloon again.
	{
		Opethighscore = Ocontinuousstroke
		"\C\1\n\n* (New high score! %(Opethighscore)!)\e"
	}
	if Dcurrentpetstreak > Dtoppetstreak //Same thing here, but it's counting the number of actual pets you got. You can use this instead of the stroke count if you like.
	{
		Dtoppetstreak = Dcurrentpetstreak
	}
	if Ocurrentpetstreak > Otoppetstreak //Same thing here, but it's counting the number of actual pets you got. You can use this instead of the stroke count if you like.
	{
		Otoppetstreak = Ocurrentpetstreak
	}
	stroke = 0
	Dcontinuousstroke = 0
	Ocontinuousstroke = 0
	Dcurrentpetstreak = 0
	Ocurrentpetstreak = 0
}

//---- OnMouseDoubleClick -------------------------------------------------------
//This is set up much like OnMouseMove above, except this is checking to see if the user is double clicking on the ghost. You also don't need to touch any of this. 
//Advanced users adding a third character, the same principles from OnMouseMove apply here as well! Make sure to add an elseif with the right number for your third character.

OnMouseDoubleClick
{
	//---- like with the OnMouseMove function above, this determines who the user is clicking on.
	if reference5 == 0 && booting != 1 //Reference5 is which mouse key is being clicked. 0 is a left click.
	{
		booting = 0
		if reference3 == 0 //if the character is 0, aka your main character
		{
			MouseDoubleClickSakura
		}
		else
		{
			OnOpenKeroMenu
		}
	}
}
 
MouseDoubleClickSakura
{
	OpenSakuraMenu
}

 
 //---- MouseDoubleClickKero -----------------------------------------------------------
 //This is just the same as MouseDoubleClickSakura above, except for your secondary character. The same principles apply, including how to make your character unhittable.
 
OnMouseDown
{
	if reference3 == 1 && reference5 == 0 && reference4 == "Head" && SHIORI3FW.CanTalk == 1 //If it's the side character, you're left clicking, if they aren't already talking, and you're on the hitbox for her head
	{
		catboops++
		"\1\s[10]* (You boop her nose.)" //I didnt use the cat noise envelope for this because I want her to use a specific frame
		"\1\s[10]* (She looks offended at your boop.)"
		--
		"\w8\w8\n\n"
		--
		"Maow"
		"Mrrp"
		"Prrp"
		"Mraa"
		"Mrr"
		"Maaow"
		"Mwwrrrr"
		--
		".\e"
	}
	elseif reference3 == 1 && reference5 == 0 && reference4 == "BEANS"
	{
		beansSquished++
		"\1\s[1008]* (You poke her toe beans!)\w8\w8\w8\s[1002]\n\n%(annoyednoises)"
		"\1\s[1008]* (You squish her toes!)\w8\w8\w8\s[1002]\n\n%(annoyednoises)"
		"\1\s[1008]* (You touch the toe beans!)\w8\w8\w8\s[1002]\n\n%(annoyednoises)"
		--
		"\w8\w8\0\s[106]...She doesn't much like her feet being touched.\e"
		"\w8\w8\n\n* (She seems annoyed.)\e"
		"\e"
		"\e"
		"\e"
		"\e"
		"\e"
	}
}

/*OnMouseDragEnd
{
	if "RYS-0825" _in_ ghostexlist && reference3 == 1 //reference3 is the character, in this case Obsidian
	{
		"\e DustyDragCoords01"
	}
}*/